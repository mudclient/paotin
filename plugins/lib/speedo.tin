#nop vim: set filetype=tt:;

/*
本文件属于 PaoTin++ 的一部分。
PaoTin++ © 2020~2022 的所有版权均由担子炮(dzp <danzipao@gmail.com>) 享有并保留一切法律权利
你可以在遵照 GPLv3 协议的基础之上使用、修改及重新分发本程序。
*/

#var lib_speedo[META] {
    {NAME}      {测速仪}
    {DESC}      {为各种数据提供测速支持，气血/精神/经验值/存款都可以}
    {AUTHOR}    {担子炮}
};

#function {lib_speedo.Init} {
    #var gSpeedo {};
    #return {true};
};

#alias {speedo.Add} {
    #local key          {%1};
    #local value        {@defaultInt{%2;0}};
    #local monotone     {@default{%3;false}};
    #local keepalive    {@defaultInt{%4;60}};

    #if { @isEmpty{$key} } {
        #return;
    };

    #math value {$value + @speedo.Get{{$key}}};
    speedo.Set {$key} {$value} {$monotone} {$keepalive};
};

#alias {speedo.Set} {
    #local key          {%1};
    #local value        {@defaultInt{%2;0}};
    #local monotone     {@default{%3;false}};
    #local keepalive    {@defaultInt{%4;60}};

    #if { @isEmpty{$key} } {
        #return;
    };

    #format {now} {%T};

    #if { "$gSpeedo[$key]" == "" } {
        #var {gSpeedo[$key][base]}          {$value};
        #var {gSpeedo[$key][baseTime]}      {$now};
        #var {gSpeedo[$key][deduction]}     {0};
        #var {gSpeedo[$key][paused]}        {false};
    };

    #nop 只允许单调递增或者单调递减;
    #if { "$monotone" == "true" } {
        #if {  ( $value > $gSpeedo[$key][value] && $gSpeedo[$key][value] < $gSpeedo[$key][base] )
            || ( $value < $gSpeedo[$key][value] && $gSpeedo[$key][value] > $gSpeedo[$key][base] ) } {
            #var {gSpeedo[$key][base]}      {$gSpeedo[$key][value]};
            #var {gSpeedo[$key][baseTime]}  {$gSpeedo[$key][update]};
            #var {gSpeedo[$key][deduction]} {0};
        };
    };

    #local interval {0};
    #math interval {$now - $gSpeedo[$key][update]};

    #nop Set 会导致继续统计，但之前暂停时经过的时间将被扣除;
    #nop 超过 keepalive 的间隔也会被扣除;
    #if { "$gSpeedo[$key][paused]" == "true" || $interval > $keepalive } {
        #var {gSpeedo[$key][paused]} {false};
        #math {gSpeedo[$key][deduction]} {$gSpeedo[$key][deduction] + $interval - $keepalive};
    };
    
    #var {gSpeedo[$key][value]}     {$value};
    #var {gSpeedo[$key][update]}    {$now};
};

#function {speedo.Get} {
    #local key {%1};

    #if { @isEmpty{$key} } {
        #return {0};
    };

    #if { "$gSpeedo[$key]" == "" } {
        #return {0};
    };

    #return {$gSpeedo[$key][value]};
};

#alias {speedo.GetUpdateTime} {
    #local key {%1};
    #return {$gSpeedo[$key][update]};
};

#function {speedo.GetSpeed} {
    #local key      {%1};
    #local interval {%2};

    #local speedInfo {@speedo.GetSpeedInfo{{$key};{$interval}}};
    #if { "$speedInfo" == "" } {
        #return {0};
    };

    #return {$speedInfo[speed]};
};

#function {speedo.GetSpeedInfo} {
    #local key      {%1};
    #local interval {@defaultInt{%2;1}};

    #if { @isEmpty{$key} } {
        #return {};
    };

    #local data {$gSpeedo[$key]};

    #if { "$data" == "" } {
        #return {};
    };

    #local delta {0};
    #math delta {$data[value] - $data[base]};

    #local speed {0};
    #math speed {
            ( $delta * $interval )
        /   ( $data[update] - $data[baseTime] - $data[deduction] )
    };

    #return {
        {begin}     {$data[baseTime]}
        {end}       {$data[update]}
        {delta}     {$delta}
        {speed}     {$speed}
        {deduction} {$data[deduction]}
    };
};

#alias {speedo.Pause} {
    #local key {%1};
    #var {gSpeedo[$key][paused]} {true};
};

#alias {speedo.Resume} {
    #local key {%1};

    #if { "$gSpeedo[$key][paused]" == "true" } {
        #var {gSpeedo[$key][paused]} {false};
        #math {gSpeedo[$key][deduction]} {$gSpeedo[$key][deduction] + $now - $gSpeedo[$key][update]};
        #format {now} {%T};
        #var {gSpeedo[$key][update]} {$now};
    };
};

#alias {speedo.Reset} {
    #local key {%1};

    #if { "$gSpeedo[$key]" == "" } {
        #return;
    };

    #format {now} {%T};

    #var {gSpeedo[$key][base]}      {0};
    #var {gSpeedo[$key][baseTime]}  {$now};
    #var {gSpeedo[$key][deduction]} {0};
    #var {gSpeedo[$key][paused]}    {false};
    #var {gSpeedo[$key][value]}     {0};
    #var {gSpeedo[$key][update]}    {$now};
};

#alias {speedo.List} {
    #local interval {@defaultInt{%1;1}};
    #local pattern  {@default{%2;{%*}}};

	#echo {<128>     %-14s %+10s %+10s %+14s %-20s %-20s %+10s} {统计对象} {统计结果} {平均值} {测算周期(秒)} {样本开始时间} {样本截止时间} {发呆时间};
	#draw Yellow scroll line 1 1 1 110;

    #local key {};
    #foreach {*gSpeedo[]} {key} {
        #if { "$key" != "$pattern" } {
            #continue;
        };

        #local speedInfo {@speedo.GetSpeedInfo{{$key};{$interval}}};
	    #echo {<168>     %-14s %+10s %+10s %+14s %t  %t  %+10s}
            {$key} {$speedInfo[delta]} {$speedInfo[speed]} {$interval}
            {{%Y-%m-%d %H:%M:%S}{$speedInfo[begin]}}
            {{%Y-%m-%d %H:%M:%S}{$speedInfo[end]}}
            {$speedInfo[deduction]};
    };
};
