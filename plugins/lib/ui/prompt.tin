#var lib_ui_prompt[META] {
    {NAME}      {æç¤ºæ æ’ä»¶}
    {DESC}      {æ”¯æŒä¸°å¯Œçš„è‡ªå®šä¹‰é€‰é¡¹ï¼Œç”¨æˆ·å¯ç”¨æ­¤æ¨¡å—å®šåˆ¶è‡ªå·±çš„ UIã€‚}
    {AUTHOR}    {æ‹…å­ç‚®}
};

/*
prompt æ’ä»¶æŠŠæ•´ä¸ªå±å¹•åˆ’åˆ†ä¸ºä¸ƒä¸ªåŒºåŸŸï¼Œä»ä¸Šåˆ°ä¸‹ä¾æ¬¡ä¸ºï¼š

Top line1
Top line2
...
Top lineN
----------- TopSepBar --------------
ï¼ˆæ¸¸æˆåŒºï¼‰
----------- MidSepBar --------------
Bot line1
Bot line2
...
Bot lineN
----------- BotSepBar --------------
<æç¤ºç¬¦>:ï¼ˆè¾“å…¥åŒºï¼‰

è¾“å…¥åŒºå‰é¢çš„æç¤ºç¬¦éƒ¨åˆ†å¯ä»¥ç”¨ API prompt.Change æ¥ä¿®æ”¹ã€‚

é™¤è¾“å…¥åŒºå’Œæ¸¸æˆåŒºä¹‹å¤–ï¼Œå‰©ä¸‹äº”ä¸ªåŒºåŸŸéƒ½å¯ä»¥å®šåˆ¶ï¼Œç”¨æ¥æ˜¾ç¤ºä¿¡æ¯å†…å®¹ï¼ˆä»¥ä¸‹ç§°ä¸ºå­—æ®µï¼‰ã€‚
æ¯ä¸€è¡Œéƒ½å¯ä»¥æœ‰å¤šä¸ªå­—æ®µã€‚å­—æ®µåœ¨è§†è§‰ä¸Šç”±ä¸¤éƒ¨åˆ†ç»„æˆï¼Œå­—æ®µæ ‡ç­¾å’Œå­—æ®µå†…å®¹ã€‚
å…¶æ˜¾ç¤ºæ ·å¼åˆ†åˆ«å¯ä»¥æ§åˆ¶ï¼Œå¹¶å—åˆ°å‡ ä¸ª prompt ç‰¹æ€§çš„å½±å“ï¼Œä¸‹é¢ä¼šåˆ†åˆ«äºˆä»¥ä»‹ç»ã€‚

å­—æ®µçš„ç¼–æ’æ˜¯é«˜åº¦å¯å®šåˆ¶çš„ï¼Œæ¯ä¸ªå­—æ®µçš„æ ‡ç­¾ã€å†…å®¹ã€é¢œè‰²ã€æ˜¾ç¤ºé£æ ¼ã€è¡Œä¸ºå±æ€§ï¼Œ
éƒ½å¯ä»¥é€šè¿‡è‡ªå·±ä¿®æ”¹ #list prompt-fields æ¥ä¿®æ”¹ã€‚ä¸‹é¢æ˜¯å¯¹ #list prompt-fields
çš„æ ¼å¼è¯´æ˜ï¼Œä½†ä¸å»ºè®®ç›´æ¥åœ¨æ­¤å¤„ä¿®æ”¹å˜é‡ï¼Œè¯·é€šè¿‡ etc/ui-settings.tin æ¥ä¿®æ”¹ã€‚
prompt æ¨¡å—åœ¨å¯åŠ¨æ—¶ä¼šè‡ªåŠ¨åŠ è½½è¯¥æ–‡ä»¶ã€‚

#list prompt-fields æ˜¯ä¸€ä¸ªåˆ—è¡¨ï¼Œå…¶ä¸­ä¸­æ¯ä¸ªå…ƒç´ æ˜¯ä¸€ä¸ª tt++ tableï¼Œä»£è¡¨ä¸€ä¸ªå­—æ®µã€‚
tt++ table ç”±ä¸€ç»„é€‰é¡¹å’Œä¸ä¹‹å¯¹åº”çš„é€‰é¡¹å€¼æ¥ç»„æˆï¼Œç”¨æ¥è¯´æ˜æƒ³è¦å®šåˆ¶çš„å­—æ®µçš„é€‰é¡¹ã€‚

ä¸‹é¢æ˜¯æ‰€æœ‰å¯ä¾›è®¾ç½®çš„å­—æ®µé€‰é¡¹ï¼š

1. place æšä¸¾å€¼ å¯é€‰é¡¹: {Top|TopSepBar|MidSepBar|Bot|BotSepBar}
  å¸Œæœ›å°†å­—æ®µæ˜¾ç¤ºåœ¨å±å¹•ä¸Šçš„ä»€ä¹ˆä½ç½®ã€‚
  å…¶ä¸­ {Top} å’Œ {Bot} æ”¯æŒå¤šè¡Œï¼Œå…¶å®ƒä¸‰ä¸ªåªæœ‰ä¸€è¡Œã€‚
  é»˜è®¤ä¸º BotSepBarã€‚

2. line è‡ªç„¶æ•°
   åªæœ‰ place={Top|Bot} æ‰æ”¯æŒ lineï¼Œä»£è¡¨ç¬¬å‡ è¡Œï¼Œæœ€ä¸Šé¢æ˜¯ç¬¬ä¸€è¡Œã€‚
   é»˜è®¤ä¸º 1ã€‚

3. order è‡ªç„¶æ•°
   åœ¨åŒä¸€è¡Œä¸­çš„æ¬¡åºï¼Œé»˜è®¤æŒ‰ç…§åœ¨ list ä¸­å‡ºç°çš„é¡ºåºæ¥æ’åºã€‚

4. label ä¸­è‹±æ–‡å­—ç¬¦ä¸²
   å­—æ®µæ ‡ç­¾ï¼Œæ¯ä¸ªå­—æ®µéƒ½å¯ä»¥æœ‰ä¸€ä¸ªæ ‡ç­¾ï¼Œæ˜¾ç¤ºåœ¨å†…å®¹å‰é¢ã€‚
   é»˜è®¤ä¸ºç©ºç™½ã€‚

5. color tintin æ ¼å¼çš„é¢œè‰²ä»£ç ï¼Œä¾‹å¦‚ <ddd>
   å­—æ®µå€¼çš„é¢œè‰²ï¼Œé€šè¿‡è¯¥é€‰é¡¹å¯ä»¥ä¸ºå­—æ®µå€¼æŒ‡å®šä¸€ä¸ªä¸é…è‰²ä¸»é¢˜ä¸ä¸€æ ·çš„é¢œè‰²ã€‚
   ä¸€èˆ¬æ¥è¯´åªæœ‰æä¸ªåˆ«é€‰é¡¹æ‰éœ€è¦ç‰¹åˆ«çš„é¢œè‰²ï¼Œæ²¡å¿…è¦ä¸ºæ¯ä¸ªé€‰é¡¹éƒ½æŒ‡å®šé¢œè‰²ï¼Œ
   å› ä¸ºé‚£æ ·çš„è¯ä½ è¿˜ä¸å¦‚å»è®¾ç½®é…è‰²ä¸»é¢˜ï¼Œå‚è§ä¸‹é¢é…è‰²ä¸»é¢˜çš„è®¾ç½®ã€‚
   é»˜è®¤ä¸ºç©ºç™½ï¼Œä»£è¡¨é‡‡ç”¨é…è‰²ä¸»é¢˜ã€‚

6. name è‹±æ–‡å­—ç¬¦ä¸² ä¸èƒ½ä¸ºç©º
   å­—æ®µåç§°ï¼Œå¿…é¡»å”¯ä¸€ï¼Œä¸èƒ½é‡å¤ã€‚

7. width æ•´æ•°
   å­—æ®µå†…å®¹çš„å®½åº¦ï¼Œå¦‚æœå­—æ®µå†…å®¹é•¿åº¦å˜åŒ–è¾ƒå¤§ï¼Œå¯ä»¥è®¾ç½®ä¸€ä¸ªå›ºå®šå®½åº¦ï¼Œä»¥å…æ™ƒåŠ¨ã€‚
   é»˜è®¤ä¸º 0ï¼Œè¡¨ç¤ºæŒ‰ç…§å†…å®¹é•¿åº¦è‡ªåŠ¨é€‚é…ã€‚

8. visibility æšä¸¾å€¼ {HideEmpty|HideCool|HideZero|HideLabel|Always}
   å«ä¹‰å¦‚ä¸‹ï¼š
    * HideEmpty å¦‚æœå­—æ®µå†…å®¹ä¸ºç©ºåˆ™ä¸äºˆæ˜¾ç¤ºã€‚
    * HideCool  å¦‚æœå†…å®¹å·²é™ˆæ—§åˆ™ä¸äºˆæ˜¾ç¤ºã€‚è¯·å‚è€ƒ cooldown é€‰é¡¹ã€‚
    * HideZero  å¦‚æœå€’è®¡æ—¶å½’é›¶åˆ™ä¸äºˆæ˜¾ç¤ºã€‚è¯·å‚è€ƒ countdown é€‰é¡¹ã€‚
    * HideLabel ä»…æ˜¾ç¤ºå­—æ®µå†…å®¹ï¼Œä¸æ˜¾ç¤ºå­—æ®µæ ‡ç­¾ã€‚
    * Always    æ€»æ˜¯æ˜¾ç¤ºè¯¥å­—æ®µçš„æ ‡ç­¾å’Œå†…å®¹ã€‚
   æ‰€æœ‰çš„ Hide* é€‰é¡¹è‡ªåŠ¨æ‹¥æœ‰ HideEmpty è¯­ä¹‰ã€‚
   é»˜è®¤ä¸º HideEmptyã€‚

9. cooldown éè´Ÿæ•´æ•°
   è¯¥å­—æ®µçš„æœ‰æ•ˆæ—¶é—´ã€‚è¶…è¿‡æœ‰æ•ˆæ—¶é—´æ²¡æœ‰æ›´æ–°çš„å­—æ®µå°†åœ¨è§†è§‰ä¸Šäºˆä»¥å¼±åŒ–æ˜¾ç¤ºï¼Œä»¥æé†’
   ç”¨æˆ·ã€‚ä¹Ÿå°±æ˜¯è¯´ï¼Œå¦‚æœ cooldown ä¸ä¸º 0ï¼Œåˆ™è¯¥å­—æ®µè¢«æ›´æ–°æ—¶å…¶æ ‡ç­¾å°†ä¼šä»¥é¢œè‰²ä¸»é¢˜
   ä¸­ HotLabel æ‰€æŒ‡å®šçš„é£æ ¼æ˜¾ç¤ºï¼ˆæ„æŒ‡æ–°é²œçš„å†…å®¹ï¼‰ï¼Œä¹‹åæŒç»­è‹¥å¹²ç§’åï¼Œè½¬ä¸ºç”±
   CoolLabel æ‰€æŒ‡å®šçš„é£æ ¼æ˜¾ç¤ºï¼ˆæ„æŒ‡é™ˆæ—§çš„å†…å®¹ï¼‰ã€‚
   æŒç»­æ—¶é—´ç”± cooldown å­—æ®µçš„å€¼æ¥å†³å®šï¼Œå•ä½ä¸ºç§’ã€‚
   é™ˆæ—§çš„å†…å®¹é‡åˆ° visibility=HideCool æ—¶åˆ™ä¸äºˆæ˜¾ç¤ºã€‚
   å¦‚æœ cooldown ä¸º 0ï¼Œåˆ™ä¸ä¼šä»¥ CoolLabel é£æ ¼æ˜¾ç¤ºã€‚
   é»˜è®¤å€¼ä¸º 0ã€‚

10. countdown æšä¸¾å€¼ {Auto|Clock|Seconds}
   å€’è®¡æ—¶ç±»å‹çš„å­—æ®µã€‚å…¶å†…å®¹ä¸ºä¸€ä¸ªéè´Ÿæ•´æ•°ï¼Œä»£è¡¨éœ€è¦å€’è®¡æ—¶çš„ç§’æ•°ï¼Œæˆ–è€…ä¹Ÿå¯ä»¥å¤¹æ‚
   äºæ–‡å­—å†…å®¹å½“ä¸­ï¼Œæ­¤æ—¶æ–‡å­—å†…å®¹ä¸­æ‰€æœ‰å½¢å¦‚ (%d) çš„å†…å®¹å°†ä¼šè¢«æ›¿æ¢æˆå€’è®¡æ—¶æ˜¾ç¤ºã€‚
   æœ¬æ’ä»¶ä¼šè‡ªåŠ¨ä¸ºè¯¥å­—æ®µæ›´æ–°å…¶å†…å®¹ï¼Œä½¿å¾—ç”¨æˆ·èƒ½å¤Ÿçœ‹åˆ°å€’è®¡æ—¶çš„æ•ˆæœã€‚
   å€’è®¡æ—¶æœ‰ä¸¤ç§æ˜¾ç¤ºæ ·å¼ï¼Œæœ¬é€‰é¡¹è®¾ç½®ä¸º Clock æ—¶ï¼Œæ˜¾ç¤ºä¸ºæ—¶é’Ÿæ ·å¼ï¼ˆNå¤©HH:MM:SSï¼‰ï¼Œ
   è®¾ç½®ä¸º Seconds æ—¶ï¼Œæ˜¾ç¤ºä¸ºè¯»ç§’æ ·å¼ï¼ˆNç§’ï¼‰ã€‚è®¾ç½®ä¸º Auto æ—¶åˆ™æ ¹æ®å‰©ä½™æ—¶é—´é•¿çŸ­ï¼Œ
   è‡ªåŠ¨åˆ‡æ¢ä¸¤ç§æ˜¾ç¤ºæ•ˆæœã€‚
   å€’è®¡æ—¶ç±»å‹çš„å­—æ®µï¼Œå¦‚æœåŒæ—¶è®¾ç½®äº† visibility=HideZeroï¼Œé‚£ä¹ˆå€’è®¡æ—¶å½’é›¶åä¼šè‡ªåŠ¨
   éšè—è¯¥å­—æ®µã€‚
   åœ¨å€’è®¡æ—¶å­˜ç»­æœŸé—´ï¼Œè¯¥å­—æ®µçš„ cooldown å±æ€§å°†ä¼šè¢«æŠ‘åˆ¶ï¼Œç›´åˆ°å€’è®¡æ—¶å½’é›¶åæ‰èµ·ä½œç”¨ã€‚
   è¿™æ¡è§„åˆ™ç¡®ä¿å€’è®¡æ—¶å­—æ®µå³ä½¿è®¾ç½®äº† visibility=HideCoolï¼Œä¹Ÿè‡³å°‘ä¼šå®Œæˆå€’è®¡æ—¶ï¼Œè€Œ
   å€’è®¡æ—¶ç»“æŸåï¼Œå¦‚æœ cooldown æ—¶é—´æ¯”å€’è®¡æ—¶æ—¶é—´è¿˜è¦é•¿ï¼Œåˆ™è¿˜ä¼šç»§ç»­æ˜¾ç¤ºä¸€æ®µæ—¶é—´ä¹‹
   åæ‰ä¼šè¢«éšè—ã€‚
*/
#var prompt-fields {};
#list prompt-fields create {};

#nop é…è‰²ä¸»é¢˜ï¼Œæ³¨æ„è¿™é‡Œä¸è¦ç›´æ¥åµŒå…¥ SGRï¼ˆansi codesï¼‰ï¼Œå¦åˆ™è®¡ç®—å®½åº¦æ—¶ä¼šæœ‰é—®é¢˜ã€‚;
#var prompt-theme {
    {Disable}{<bbc>}
    {BusyColor}{<030>}
    {BattleColor}{<110>}
    {BattleBusyColor}{<500><110>}
    {TopSepBar}{<020>}
    {MidSepBar}{<020>}
    {BotSepBar}{<020>}
    {HotLabel}{<100><F399>}
    {CoolLabel}{<100>}
    {Value}{<070>}
};

#nop è‡ªå®šä¹‰å›¾æ ‡;
#var prompt-icon {
    {DisableRefresh}{ğŸš«}
};

#nop çƒ­é”®ç»‘å®š;
#var global-key-bindings {};
#list global-key-bindings create {
    {{key}{\cos}    {action}{prompt.ToggleSwitch}}
};

#var prompt-top-max-line {0};
#var prompt-bot-max-line {0};

#nop æ‰€æœ‰çš„å­—æ®µå€¼çš„å­—å…¸;
#var prompt-dict {};

#nop ä¸Šæ¬¡å®é™…ç»˜åˆ¶å±å¹•çš„æ—¶é—´;
#var prompt-refresh {
    {global}{false}
    {lines} {false}
    {prompt}{false}
};

#nop æç¤ºç¬¦;
#var prompt-prompt {Input};

#function {lib_ui_prompt.Init} {
    load-file {etc/ui-settings.tin};
    prompt.Set {};
    prompt.bindKey;
    prompt.Enable;

    #return true;
};

#nop è®¾ç½®å­—æ®µå€¼ï¼Œè¢«è®¾ç½®çš„å­—æ®µå€¼å°†ç«‹å³æ˜¾ç¤ºåœ¨å±å¹•ä¸Š;
#alias {prompt.Set} {
    #local fields   {%1};

    #local now {};
    #format now {%T};

    #local field    {};
    #foreach {*fields[]} {field} {
        #var prompt-dict[$field] {
            {updateTime}{$now}
            {showTime}{$now}
            {value}{$fields[$field]}
        };
    };
};

#nop è®¾ç½®æç¤ºç¬¦ï¼Œæç¤ºç¬¦ä½äºå±å¹•æœ€ä¸‹æ–¹è¾“å…¥åŒºçš„é¦–éƒ¨ï¼Œç”¨ä½œæŒ‡ç¤ºæ•´ä¸ªæ¸¸æˆæ‰€å¤„çš„çŠ¶æ€;
#alias {prompt.Change} {
    #local text {%1};

    #var prompt-prompt {$text};
    #echo {{\r\e[K$prompt-prompt}{-1}};
};

#nop è®¾ç½®å­—æ®µå€¼ï¼Œè¢«è®¾ç½®çš„å­—æ®µå€¼å°†ç«‹å³æ˜¾ç¤ºåœ¨å±å¹•ä¸Šï¼Œä½†ä¼šåœ¨ä¸€æ®µæ—¶é—´åæ¶ˆå¤±;
#alias {prompt.SetAwhile} {
    #local id    {%1};
    #local value {%2};
    #local secs  {%3};

    #if { "$secs" == "" } {
        #local secs {5};
    };

    prompt.Set {{$id}{$value}};

    #line sub {VARIABLES} {
        #delay prompt-set-awhile-$id {
            prompt.Set {{$id}{}};
        } {$secs};
    };
};

#nop æ˜¾ç¤ºå­—æ®µå€¼;
#alias {prompt.refresh} {
    #local topMaxLine   {0};
    #local botMaxLine   {0};

    #local topLines     {};
    #local botLines     {};
    #local topSepBar    {};
    #local midSepBar    {};
    #local botSepBar    {};

    #foreach {*{prompt-fields[]}} {idx} {
        #local field {${prompt-fields[$idx]}};
        #if { "$field[visibility]" == "" } {
            #local field[visibility] {HideEmpty};
        };
        #switch {"$field[place]"} {
            #case {"Top"} {
                #while {1} {
                    #if { $field[line] <= $topMaxLine } {
                        #break;
                    };
                    #math topMaxLine {$topMaxLine + 1};
                    #local topLines[$topMaxLine] {};
                };
                #if { "$field[order]" == "" } {
                    #local field[order] {@eval{ @max{0;*topLines[$field[line]][]} + 1 }};
                };
                #local topLines[$field[line]][$field[order]] {$field};
            };
            #case {"Bot"} {
                #while {1} {
                    #if { $field[line] <= $botMaxLine } {
                        #break;
                    };
                    #math botMaxLine {$botMaxLine + 1};
                    #local botLines[$botMaxLine] {};
                };
                #if { "$field[order]" == "" } {
                    #local field[order] {@eval{ @max{0;*botLines[$field[line]][]} + 1 }};
                };
                #local botLines[$field[line]][$field[order]] {$field};
            };
            #case {"TopSepBar"} {
                #if { "$field[order]" == "" } {
                    #local field[order] {@eval{ @max{0;*topSepBar[]} + 1 }};
                };
                #local topSepBar[$field[order]] {$field};
            };
            #case {"MidSepBar"} {
                #if { "$field[order]" == "" } {
                    #local field[order] {@eval{ @max{0;*midSepBar[]} + 1 }};
                };
                #local midSepBar[$field[order]] {$field};
            };
            #case {"BotSepBar"} {
                #if { "$field[order]" == "" } {
                    #local field[order] {@eval{ @max{0;*botSepBar[]} + 1 }};
                };
                #local botSepBar[$field[order]] {$field};
            };
            #default {
                #echo {é…ç½®æœ‰è¯¯ï¼Œè¯·æ£€æŸ¥ã€‚place=[%s]} {$field[place]};
            };
        };
    };

    #local allBarColor {};
    #if { "${prompt-dict[busy][value]}" == "true" } {
        #local allBarColor {${prompt-theme[BusyColor]}};
    };
    #if { "${prompt-dict[battle][value]}" == "true" } {
        #local allBarColor {${prompt-theme[BattleColor]}};
    };
    #if { "${prompt-dict[battle][value]}" == "true" && "${prompt-dict[busy][value]}" == "true" } {
        #local allBarColor {${prompt-theme[BattleBusyColor]}};
    };
    #if { "${prompt-dict[disable][value]}" != "" } {
        #local allBarColor {${prompt-theme[Disable]}};
    };

    #if { &topSepBar[] > 0 } {
        #math topMaxLine {$topMaxLine + 1};
    };

    #if { $botMaxLine == 0 && ( &midSepBar[] == 0 || &midSepBar[] == 0 ) } {
        #local botMaxLine {1};
    };
    #else {
        #math botMaxLine {$botMaxLine + 2};
    };

    #local content {};
    #list content create {};

    #local line {};
    #local delta {0};
    #foreach {*topLines[]} {line} {
        #local fields {$topLines[$line]};
        #local text {@__prompt_build_line__{{$fields}}};
        #if { $text[width] > 0 } {
            #local realLine {};
            #math realLine {$line - $delta};
            #list content {add} {{{line}{$realLine}{text}{$text[text]}}};
        };
        #else {
            #math delta {$delta + 1};
            #math topMaxLine {$topMaxLine - 1};
        };
    };

    #if { &topSepBar[] > 0 } {
        #local text {@__prompt_build_line__{{$topSepBar}}};
        #if { $text[width] == 0 && $topMaxLine == 1 } {
            #math topMaxLine {$topMaxLine - 1};
        };
        #else {
            #local barColor {${prompt-theme[TopSepBar]}};
            #if { "$allBarColor" != "" } {#local barColor {$allBarColor}};
            #local text {@__prompt_fill_line__{{$text[text]};{$text[width]};$barColor}};
            #list content {add} {{{line}{$topMaxLine}{text}{$text}}};
        };
    };

    #if { &botSepBar[] > 0 || $botMaxLine > 0 } {
        #local text {@__prompt_build_line__{{$botSepBar}}};
        #local barColor {${prompt-theme[BotSepBar]}};
        #if { "$allBarColor" != "" } {#local barColor {$allBarColor}};
        #local text {@__prompt_fill_line__{{$text[text]};{$text[width]};$barColor}};
        #list content {add} {{{line}{-2}{text}{$text}}};
    };

    #local delta {0};
    #local line {0};
    #if { &botLines[] > 0 } {
        #loop {&botLines[]} {1} {line} {
            #local fields {$botLines[$line]};
            #local text {@__prompt_build_line__{{$fields}}};
            #if { $text[width] > 0 } {
                #local realLine {};
                #math realLine {$line - $botMaxLine - 1};
                #list content {add} {{{line}{$realLine}{text}{$text[text]}}};
            };
            #else {
                #math botMaxLine {$botMaxLine - 1};
            };
        };
    };

    #if { &midSepBar[] > 0 || &botLines[] > 0 } {
        #local text {@__prompt_build_line__{{$midSepBar}}};
        #local barColor {${prompt-theme[MidSepBar]}};
        #if { "$allBarColor" != "" } {#local barColor {$allBarColor}};
        #local text {@__prompt_fill_line__{{$text[text]};{$text[width]};$barColor}};
        #math line {$botMaxLine + 1};
        #list content {add} {{{line}{-$line}{text}{$text}}};
    };

    #if { "${prompt-top-max-line}" != "$topMaxLine" || "${prompt-bot-max-line}" != "$botMaxLine" } {
        #local lineWidth {};
        #screen get COLS lineWidth;
        #local spaceLine {};
        #format spaceLine {%-${lineWidth}s} {};

        #local newMax {$topMaxLine};
        #while { $newMax < ${prompt-top-max-line} } {
            #math newMax {$newMax + 1};
            #echo {{$spaceLine}{$newMax}};
        };
        #local newMax {$botMaxLine + 1};
        #while { $newMax < ${prompt-bot-max-line} } {
            #math newMax {$newMax + 1};
            #echo {{$spaceLine}{-$newMax}};
        };

        #var prompt-top-max-line {$topMaxLine};
        #var prompt-bot-max-line {$botMaxLine};
        #split {$topMaxLine} {$botMaxLine};
    };

    #local idx {};
    #foreach {*content[]} {idx} {
        #local line {$content[$idx]};
        #echo {{%s}{$line[line]}} {$line[text]};
    };

    #local prompt {${prompt-prompt}};
    #if { "$prompt" != "" } {
        #local prompt {$prompt};
        #echo {{$prompt}{-1}};
    };
};

#function {__prompt_build_line__} {
    #local fields       {%1};
    #local text         {};
    #local order        {};
    #local lineWidth    {0};
    #foreach {*fields[]} {order} {
        #local field {$fields[$order]};
        #local name  {$field[name]};
        #local label {$field[label]};
        #local color {$field[color]};
        #local width {$field[width]};
        #local value {${prompt-dict[$name]}};
        #local now {};
        #format now {%T};

        #nop æ‰€æœ‰çš„ Hide* é€‰é¡¹è‡ªåŠ¨æ‹¥æœ‰ HideEmpty è¯­ä¹‰ã€‚;
        #if { "$field[visibility]" == "Hide%*" && "$value[value]" == "" } {
            #continue;
        };

        #nop æ£€æŸ¥æ˜¯å¦æ˜¯å€’è®¡æ—¶ï¼Œä»¥åŠå€’è®¡æ—¶æ˜¯å¦å·²ç»å½’é›¶;
        #local zero {undef};
        #if { "$field[countdown]" != "" } {
            #local seconds {};
            #math seconds {$now - ${prompt-dict[$name][showTime]}};
            #replace value[value] {^%+1..d$} {@__prompt_countdown__{&1;$seconds}};
            #replace value[value] {(%+1..d)} {(@__prompt_countdown__{&1;$seconds})};
            #if { "$value[value]" != "{[1-9][0-9]*|.*\([1-9][0-9]*\).*}" } {
                #local zero {true};
            };
            #else {
                #local zero {false};
            };
            #var {prompt-dict[$name][showTime]} {$now};
            #var {prompt-dict[$name][value]} {$value[value]};
            #replace value[value] {^%+1..d$} {@__prompt_show_countdown__{$field[countdown];&1}};
            #replace value[value] {(%+1..d)} {(@__prompt_show_countdown__{$field[countdown];&1})};
        };

        #nop å¦‚æœè®¾ç½®äº† HideZeroï¼Œé‚£ä¹ˆå€’è®¡æ—¶å½’é›¶æ—¶ï¼Œä¸äºˆæ˜¾ç¤ºè¯¥å­—æ®µã€‚;
        #if { "$field[visibility]" == "HideZero" && "$zero" == "true" } {
            #continue;
        };

        #nop æ£€æŸ¥æ˜¯å¦å·²ç»æ˜¯å†·å´äº†çš„å­—æ®µï¼Œå€’è®¡æ—¶å­—æ®µå½“å€’è®¡æ—¶æŒç»­æ—¶ä¸ä¼šå˜ä¸ºå†·å´;
        #local cool {false};
        #if { $field[cooldown] > 0 && $now - $value[updateTime] > $field[cooldown] && "$zero" != "false" } {
            #local cool {true};
        };

        #nop å¦‚æœè®¾ç½®äº† HideCoolï¼Œé‚£ä¹ˆå†…å®¹å†·å´ä¹‹ååˆ™ä¸äºˆæ˜¾ç¤º;
        #if { "$field[visibility]" == "HideCool" && "$cool" == "true" } {
            #continue;
        };

        #nop å¦‚æœè®¾ç½®äº† HideLabelï¼Œé‚£ä¹ˆä¸æ˜¾ç¤ºæ ‡ç­¾;
        #if { "$field[visibility]" == "HideLabel" } {
            #local label {};
        };

        #if { "$label" != "" } {
            #nop å¦‚æœå…¨å±€å¼€å…³å·²ç»ç¦ç”¨ï¼Œåˆ™å¿½ç•¥æ‰€æœ‰é…è‰²ï¼Œå…¨éƒ¨æ˜¾ç¤ºä¸ºç¦ç”¨è‰²;
            #if { "${prompt-dict[disable][value]}" != "" } {
                #replace label {^<{[a-zA-Z0-9]+}>} {};
                #local label {${prompt-theme[Disable]}$label};
            };
            #else {
                #nop å¦åˆ™æ ¹æ®å†…å®¹çš„æ–°é²œç¨‹åº¦è‡ªåŠ¨æ”¹å˜é¢œè‰²;
                #if { "$cool" == "true" } {
                    #replace label {^<{[a-zA-Z0-9]+}>} {};
                    #local label {${prompt-theme[CoolLabel]}$label};
                };
                #else {
                    #local label {${prompt-theme[HotLabel]}$label};
                };
            };

            #local label {$label<070> };
        };

        #if { "$field[visibility]" == "HideLabel" } {
            #local label {};
        };

        #if { "$color" == "" } {
            #local color {${prompt-theme[Value]}};
        };

        #format value {$color%-${width}s<070>} {$value[value]};
        #math lineWidth {$lineWidth + @strWidth{$label} + @strWidth{$value}};
        #if { "$text" == "" } {
            #local text {$label$value};
        };
        #elseif { "$label$value" != "" } {
            #local text {$text $label$value};
            #math lineWidth {$lineWidth + 1};
        };
    };

    #return {{width}{$lineWidth}{text}{$text}};
};

#function {__prompt_countdown__} {
    #local value    {%1};
    #local count    {%2};

    #math value {$value - $count};
    #if { $value < 0 } {
        #local value {0};
    };

    #return {$value};
};

#function {__prompt_show_countdown__} {
    #local type     {%1};
    #local secs     {%2};
    #local ret      {};

    #if { "$type" == "Auto" } {
        #if { $secs < 600 } {
            #local type {Seconds};
        };
        #else {
            #local type {Clock};
        };
    };

    #if { "$type" == "Seconds" } {
        #local ret {${secs}s};
    };
    #elseif { "$type" == "Clock" } {
        #if { $secs > 86400 } {
            #math ret {$secs / 86400};
            #local ret {$retå¤©};
            #math secs {$secs % 86400};
        };

        #local hour {};
        #local mins {};
        #math hour  {$secs / 3600};
        #math secs  {$secs % 3600};
        #math mins  {$secs / 60};
        #math secs  {$secs % 60};
        #format ret {%s%%02d:%%02d:%%02d} {$ret} {$hour} {$mins} {$secs};
    };

    #return {$ret};
};

#function {__prompt_fill_line__} {
    #local text     {%1};
    #local width    {%2};
    #local color    {%3};

    #local newText {@trim{$text}};
    #math width {$width + @strWidth{$newText} - @strWidth{$text}};

    #local screenWidth {0};
    #screen get COLS screenWidth;

    #local leftLen {0};
    #local rightLen {0};
    #math leftLen {($screenWidth - $width) / 2 - 1};
    #math rightLen {$screenWidth - $leftLen - $width - 2};

    #local left {};
    #format {left} {%${leftLen}s} {};
    #replace {left} { } {â”€};

    #local right {};
    #format {right} {%${rightLen}s} {};
    #replace {right} { } {â”€};

    #if { $leftLen < 0 } {
        #format {newText} {%$screenWidth.${screenWidth}s} {$newText};
        #return {$newText};
    };
    #elseif { "$newText" == "" } {
        #return {$color$leftâ”€â”€$right<070>};
    };
    #else {
        #return {$color$left<070> $newText $color$right<070>};
    };
};

#event {SCREEN RESIZE} {
    #var prompt-top-max-line {0};
    #var prompt-bot-max-line {0};
    prompt.refresh;
    #buffer end;
};

#event {CATCH IAC SB NAWS} {#0};

#alias {prompt.UpdateHP} {
    prompt.Set {
        {exp}{$char[HP][ç»éªŒæ˜¾ç¤º]}
        {pot}{$char[HP][æ½œèƒ½æ˜¾ç¤º]}
        {battle}{$char[HP][æˆ˜æ–—ä¸­]}
        {busy}{$char[HP][å¿™]}
    }
};

#alias {prompt.UpdateSM} {
    #local effect {$char[STATUS][æŒç»­æ•ˆæœ]};
    #replace effect {ç§’)} {)};

    prompt.Set {
        {yunqi}{$char[STATUS][æ°”è¡€æ¢å¤]}
        {status}{$char[STATUS][å¥åº·çŠ¶æ€]}
        {persist}{$effect}
    }
};

#alias {prompt.Disable} {
    prompt.Set {{disable}{${prompt-icon[DisableRefresh]}}};
    prompt.refresh;
    #untick prompt.refresh;
};

#alias {prompt.Enable} {
    prompt.Set {{disable}{}};
    Tick prompt.refresh {prompt.refresh} 1;
};

#alias {prompt.ToggleSwitch} {
    #if { "${prompt-dict[disable][value]}" == "" } {
        prompt.Disable;
    };
    #else {
        prompt.Enable;
    };
};

#alias {prompt.bindKey} {
    #local idx {};
    #foreach {*{global-key-bindings[]}} {idx} {
        #local key  {${global-key-bindings[$idx][key]}};
        #local code {${global-key-bindings[$idx][action]}};
        #line sub var #macro {$key} {$code};
    };
};

#alias {prompt.test} {
    #local fullme {<010>å¾ˆä¹…æ²¡æœ‰è¿›è¡Œæœºå™¨äººæ£€æŸ¥(fullme)äº†ï¼Œä»»åŠ¡å¥–åŠ±å°†å—åˆ°å½±å“ã€‚<070>};
    #local fullme {http://pkuxkx.com/antirobot/robot.php?filename=1576762922984895};
    prompt.Set { {URL}{$fullme} };
    prompt.Set {
        {pot}{357.75ä¸‡}
        {exp}{7510.56ä¸‡}
        {expSpd}{11.88ä¸‡/å°æ—¶}
        {profit}{308é‡‘å¸/å°æ—¶}
    };
    prompt.Set {
        {job}{<171>ä¹”å³°}
        {stage}{å¯»æ‰¾NPC}
        {area}{å»ºåº·}
        {room}{ä¸­åŸ}
        {npc}{åºä¹å…¬}
        {type}{æ€æ­»}
    };
};
