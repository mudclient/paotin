#nop vim: set filetype=tt:;

/*
本文件属于 PaoTin++ 的一部分。
PaoTin++ © 2020~2023 的所有版权均由担子炮(dzp <danzipao@gmail.com>) 享有并保留一切法律权利
你可以在遵照 GPLv3 协议的基础之上使用、修改及重新分发本程序。
*/

#nop 本文件是 xtintin 的一部分，实现了一些集合处理函数;

///=== {
///// 集合处理函数：
/////
///// 集合在 TinTin++ 中并无原生概念对应，PaoTin++ 中的集合用 #list 来实现，保证每个元素都不相同。
///// 也就是说，无论是对集合进行何种运算，重复元素在集合中只会出现一次。
/////
///// 由于 PaoTin++ 中是用 #list 来存储 set，因此 #list 相关的命令和 xtintin API 都可以用于集合。
//
// #@ set.Create <元素> [...]
//    利用提供的多个元素创建新集合。
//
//    EXAMPLE: \@set.Create{a;b;c}
//    RESULT:  {{1}{a}{2}{b}{3}{c}}
//
//    EXAMPLE: \@set.Create{h;e;l;l;o}
//    RESULT:  {{1}{h}{2}{e}{3}{l}{4}{o}}
// };
#func {set.Create} {
    #return {@set.Add{{};%0}};
};

///=== {
// #@ set.FromList <列表>
//    将列表转换成集合。
//
//    EXAMPLE:
//      #local list {};
//      #list list {create} {h;e;l;l;o};
//      #local set {\@set.FromList{$list}};
//
//    ASSERT: {$set} === {{1}{h}{2}{e}{3}{l}{4}{o}}
// };
#func {set.FromList} {
    #local list {%0};
    #return {@set.Add{{};$list[%*]}};
};

///=== {
// #@ set.Size <集合>
//    计算集合的元素数量。
//
//    EXAMPLE:
//      #local set {\@set.Create{h;e;l;l;o}};
//      #local size {\@set.Size{$set}};
//
//    ASSERT: {$size} === {4}
// };
#func {set.Size} {
    #local set {%0};
    #return &set[];
};

///=== {
// #@ set.Contains <集合> <元素>
//    判断元素是否在集合中。
//
//    EXAMPLE:
//      #if {   \@set.Contains{{\@set.Create{a;b;c}};{b}}   } {#echo OK1};
//      #if { ! \@set.Contains{{\@set.Create{a;b;c}};{x}}   } {#echo OK2};
//      #if { ! \@set.Contains{{};{x}}                      } {#echo OK3};
//
//    OUTPUT:
//      OK1
//      OK2
//      OK3
// };
#func {set.Contains} {
    #info arguments save;
    #local set  {$info[ARGUMENTS][1]};
    #local elem {$info[ARGUMENTS][2]};

    #if { @list.IndexOf{{$set};{$elem}} > 0 } {
        #return 1;
    };
    #else {
        #return 0;
    };
};

///=== {
// #@ set.Add <集合> <元素> [...]
//    将一个或者多个元素添加到集合当中。
//
//    EXAMPLE:
//      #local set {};
//      #local set {\@set.Add{{$set}; a}};
//      #local set {\@set.Add{{$set}; b; c}};
//      #nop 注意：因为是集合，所以重复元素不再添加;
//      #local set {\@set.Add{{$set}; a; b; c}};
//      #local set {\@set.Add{{$set}; x; y; z}};
//      #nop 注意：这里也有个重复的 l，只会被添加一次;
//      #local set {\@set.Add{{$set}; h; e; l; l; o}};
//
//    ASSERT: {$set} === {{1}{a}{2}{b}{3}{c}{4}{x}{5}{y}{6}{z}{7}{h}{8}{e}{9}{l}{10}{o}}
// };
#func {set.Add} {
    #info arguments save;
    #unvar info[ARGUMENTS][0];
    #local argv {$info[ARGUMENTS]};

    #if { &argv[] < 2 } {
        #return {$argv[1]};
    };

    #local set  {$argv[1]};
    #local idx  {0};
    #loop {2} {&argv[]} {idx} {
        #local elem {$argv[+$idx]};
        #if { !@set.Contains{{$set};{$elem}} } {
            #list set {add} {{$elem}};
        };
    };

    #return {$set};
};

///=== {
// #@ set.Remove <集合> <元素> [...]
//    从集合中删除一个或者多个元素。
//
//    EXAMPLE:
//      #local set {\@set.Create{a;b;c;d;e}};
//      #local set {\@set.Remove{{$set};b;c;x;y;x}};
//
//    ASSERT: {$set} === {{1}{a}{2}{d}{3}{e}}
// };
#func {set.Remove} {
    #info arguments save;
    #unvar info[ARGUMENTS][0];
    #local argv {$info[ARGUMENTS]};

    #if { &argv[] < 2 } {
        #return {$argv[1]};
    };

    #local set  {$argv[1]};
    #local idx  {0};
    #loop {2} {&argv[]} {idx} {
        #local elem {$argv[+$idx]};
        #local idx @list.IndexOf{{$set};{$elem}};
        #if { $idx > 0 } {
            #list set {delete} {$idx};
        };
    };

    #return {$set};
};

///=== {
// #@ set.Equal <集合1> <集合2>
//    判断集合1和集合2是否相等。
//    📖  如果两个集合中包含的元素是相同的，那么就判定为相等。相等判定并不要求元素顺序相同。
//
//    EXAMPLE:
//      #local set1 {\@set.Create{a;b;c}};
//      #local set2 {};
//      #local set2 {\@set.Add{{$set2};a}};
//      #local set2 {\@set.Add{{$set2};b}};
//      #local set2 {\@set.Add{{$set2};c}};
//      #local ok {\@set.Equal{{$set1};{$set2}}};
//
//    ASSERT: {$ok} === {1}
// };
#func {set.Equal} {
    #local set1  {%1};
    #local set2  {%2};

    #if { @set.IsSubset{{$set1};{$set2}} && &set1[] == &set2[] } {
        #return 1;
    };
    #else {
        #return 0;
    };
};

///=== {
// #@ set.IsSubset <集合1> <集合2>
//    判断集合2是否为集合1的子集。
//    📖  如果集合1包含了集合2中的每个元素，那么集合2就是集合1的子集。
//
//    EXAMPLE:
//      #if {   \@set.IsSubset{{\@set.Create{a;b;c;d;e}};{\@set.Create{a;c;e}}} } {#echo OK1};
//      #if { ! \@set.IsSubset{{\@set.Create{a;b;c;d;e}};{\@set.Create{a;b;X}}} } {#echo OK2};
//      #if {   \@set.IsSubset{{\@set.Create{a;b;c}};{\@set.Create{a;b;c}}}     } {#echo OK3};
//      #if {   \@set.IsSubset{{\@set.Create{a;b;c}};{}}                        } {#echo OK4};
//      #if {   \@set.IsSubset{{};{}}                                           } {#echo OK5};
//
//    OUTPUT:
//      OK1
//      OK2
//      OK3
//      OK4
//      OK5
// };
#func {set.IsSubset} {
    #local set1  {%1};
    #local set2  {%2};

    #local idx {0};
    #foreach {*set2[]} {idx} {
        #if { !@set.Contains{{$set1};{$set2[$idx]}} } {
            #return 0;
        };
    };

    #return 1;
};

///=== {
// #@ set.IsProperSubset <集合1> <集合2>
//    判断集合2是否为集合1的真子集。
//    📖  如果集合1包含了集合中2的每个元素，而且集合1比集合2的元素还要多，
//       那么集合2就是集合1的真子集。
//
//    EXAMPLE:
//      #if {   \@set.IsProperSubset{{\@set.Create{a;b;c;d;e}};{\@set.Create{a;c;e}}}   } {#echo OK1};
//      #if { ! \@set.IsProperSubset{{\@set.Create{a;b;c;d;e}};{\@set.Create{a;b;X}}}   } {#echo OK2};
//      #if { ! \@set.IsProperSubset{{\@set.Create{a;b;c}};{\@set.Create{a;b;c}}}       } {#echo OK3};
//      #if {   \@set.IsProperSubset{{\@set.Create{a;b;c}};{}}                          } {#echo OK4};
//      #if { ! \@set.IsProperSubset{{};{}}                                             } {#echo OK5};
//
//    OUTPUT:
//      OK1
//      OK2
//      OK3
//      OK4
//      OK5
// };
#func {set.IsProperSubset} {
    #local set1  {%1};
    #local set2  {%2};

    #if { @set.IsSubset{{$set1};{$set2}} && &set1[] > &set2[] } {
        #return 1;
    };
    #else {
        #return 0;
    };
};

///=== {
// #@ set.IsSuperset <集合1> <集合2>
//    判断集合2是否为集合1的超集。
//    📖  如果集合2包含了集合1中的每个元素，那么集合2就是集合1的超集。
//
//    EXAMPLE:
//      #if {   \@set.IsSuperset{{\@set.Create{a;c;e}};{\@set.Create{a;b;c;d;e}}}   } {#echo OK1};
//      #if { ! \@set.IsSuperset{{\@set.Create{a;b;X}};{\@set.Create{a;b;c;d;e}}}   } {#echo OK2};
//      #if {   \@set.IsSuperset{{\@set.Create{a;b;c}};{\@set.Create{a;b;c}}}       } {#echo OK3};
//      #if {   \@set.IsSuperset{{};{\@set.Create{a;b;c}}}                          } {#echo OK4};
//      #if {   \@set.IsSuperset{{};{}}                                             } {#echo OK5};
//
//    OUTPUT:
//      OK1
//      OK2
//      OK3
//      OK4
//      OK5
// };
#func {set.IsSuperset} {
    #return {@set.IsSubset{{%2};{%1}}};
};

///=== {
// #@ set.IsProperSuperset <集合1> <集合2>
//    判断集合2是否为集合1的真超集。
//    📖  如果集合2包含了集合中1的每个元素，而且集合2比集合1的元素还要多，
//       那么集合2就是集合1的真超集。
//
//    EXAMPLE:
//      #if {   \@set.IsProperSuperset{{\@set.Create{a;c;e}};{\@set.Create{a;b;c;d;e}}} } {#echo OK1};
//      #if { ! \@set.IsProperSuperset{{\@set.Create{a;b;X}};{\@set.Create{a;b;c;d;e}}} } {#echo OK2};
//      #if { ! \@set.IsProperSuperset{{\@set.Create{a;b;c}};{\@set.Create{a;b;c}}}     } {#echo OK3};
//      #if {   \@set.IsProperSuperset{{};{\@set.Create{a;b;c}}}                        } {#echo OK4};
//      #if { ! \@set.IsProperSuperset{{};{}}                                           } {#echo OK5};
//
//    OUTPUT:
//      OK1
//      OK2
//      OK3
//      OK4
//      OK5
// };
#func {set.IsProperSuperset} {
    #return {@set.IsProperSubset{{%2};{%1}}};
};

///=== {
// #@ set.Union <集合1> <集合2> [...]
//    求两个或者多个集合的并集。
//    📖  并集是指把所有集合元素放在一块儿之后得到的新集合。
//
//    EXAMPLE: \@set.Union{{\@set.Create{a;b;c}};{\@set.Create{b;c;d}};{\@set.Create{c;d;e}}}
//    RESULT: {{1}{a}{2}{b}{3}{c}{4}{d}{5}{e}}
// };
#func {set.Union} {
    #info arguments save;
    #unvar info[ARGUMENTS][0];
    #local argv {$info[ARGUMENTS]};

    #if { &argv[] < 2 } {
        #return {$argv[1]};
    };

    #local set  {$argv[1]};
    #local idx  {0};
    #loop {2} {&argv[]} {idx} {
        #local other {$argv[+$idx]};
        #local set {@set.Add{{$set};$other[%*]}};
    };

    #return {$set};
};

///=== {
// #@ set.Intersection <集合1> <集合2> [...]
//    求两个或者多个集合的交集。
//    📖  交集是指由同时出现在所有集合中的元素组成的新集合。
//
//    EXAMPLE: \@set.Intersection{{\@set.Create{a;b;c}};{\@set.Create{b;c;d}};{\@set.Create{c;d;e}}}
//    RESULT: {{1}{c}}
// };
#func {set.Intersection} {
    #info arguments save;
    #unvar info[ARGUMENTS][0];
    #local argv {$info[ARGUMENTS]};

    #if { &argv[] < 2 } {
        #return {};
    };

    #local set  {$argv[1]};
    #local idx  {0};
    #loop {2} {&argv[]} {idx} {
        #if { &set[] == 0 } {
            #return {};
        };

        #local other {$argv[+$idx]};
        #loop {&set[]} {1} {idx} {
            #local elem {$set[$idx]};
            #if { !@set.Contains{{$other};{$elem}} } {
                #list set {delete} {$idx};
            };
        };
    };

    #return {$set};
};

///=== {
// #@ set.IsDisjoint <集合1> <集合2> [...]
//    判断两个或者多个集合是否为不交集。
//    📖  不交集是交集为空的意思。
//
//    EXAMPLE:
//      #if {   \@set.IsDisjoint{{\@set.Create{a;b;c}};{\@set.Create{b;c;d}};{\@set.Create{X;d;e}}} } {#echo OK1};
//      #if { ! \@set.IsDisjoint{{\@set.Create{a;b;c}};{\@set.Create{b;c;d}};{\@set.Create{c;d;e}}} } {#echo OK2};
//
//    OUTPUT:
//      OK1
//      OK2
// };
#func {set.IsDisjoint} {
    #local set {@set.Intersection{%0}};
    #if { &set[] == 0 } {
        #return 1;
    };
    #else {
        #return 0;
    };
};

///=== {
// #@ set.Product <集合1> <集合2> [...]
//    计算两个或者多个集合的笛卡尔积。返回结果集合中的每个元素都是一个字符串列表，
//    其值分别来自各个参数集合的元素。
//    📖  两个集合的笛卡尔积是指由两个集合的元素组成的所有可能的有序对的集合。
//       多个集合的积也可由此扩展。
//
//    EXAMPLE: \@set.Product{{\@set.Create{扬州;苏州}};{\@set.Create{钱庄;当铺}}}
//    RESULT: {{1}{扬州;钱庄}{2}{扬州;当铺}{3}{苏州;钱庄}{4}{苏州;当铺}}
// };
#func {set.Product} {
    #info arguments save;
    #unvar info[ARGUMENTS][0];
    #local argv {$info[ARGUMENTS]};

    #if { &argv[] < 2 } {
        #return {};
    };

    #local set  {$argv[1]};
    #if { &set[] == 0 } {
        #return {};
    };

    #local idx  {0};
    #loop {2} {&argv[]} {idx} {
        #local other {$argv[+$idx]};
        #if { &other[] == 0 } {
            #return {};
        };

        #local newSet {};
        #local left {};
        #local right {};
        #loop {1} {&set[]} {left} {
            #local left {$set[+$left]};
            #loop {1} {&other[]} {right} {
                #local right {$other[+$right]};
                #list newSet {add} {{$left;$right}};
            };
        };

        #local set {$newSet};
    };

    #return {$set};
};

///=== {
// #@ set.Diff <集合1> <集合2> [...]
//    计算集合1与其它集合的相对差。
//    📖  相对差是指从集合1中去掉所有在其它集合中出现过的元素之后剩下的集合。
//
//    EXAMPLE:  \@set.Diff{{\@set.Create{a;b;c;d;e}};{\@set.Create{c;d;e;f;g}}}
//    RESULT:   {{1}{a}{2}{b}}
//
//    EXAMPLE:  \@set.Diff{{\@set.Create{a;b;c;d;e;f}};{\@set.Create{a;c}};{\@set.Create{d;g}};{\@set.Create{f;x}}}
//    RESULT:   {{1}{b}{2}{e}}
// };
#func {set.Diff} {
    #info arguments save;
    #unvar info[ARGUMENTS][0];
    #local argv {$info[ARGUMENTS]};

    #if { &argv[] < 2 } {
        #return {$argv[1]};
    };

    #local set  {$argv[1]};
    #local idx  {0};
    #loop {2} {&argv[]} {idx} {
        #if { &set[] == 0 } {
            #return {};
        };

        #local other {$argv[+$idx]};
        #local set {@set.Remove{{$set};$other[%*]}};
    };

    #return {$set};
};

///=== {
// #@ set.SymmDiff <集合1> <集合2>
//    计算集合1与集合2的对称差。
//    📖  对称差是指并集与交集的相对差。
//
//    EXAMPLE:  \@set.SymmDiff{{\@set.Create{a;b;c;d;e}};{\@set.Create{c;d;e;f;g}}}
//    RESULT:   {{1}{a}{2}{b}{3}{f}{4}{g}}
// };
#func {set.SymmDiff} {
    #return {@set.Diff{{@set.Union{{%1};{%2}}};{@set.Intersection{{%1};{%2}}}}};
};

///=== {
///// 下面这一组 API 采用字符串列表来作为集合表达。功能与上面相同。只是数据类型不一样。
///// 为了和上面以示区分，前缀 set 改为 sset，意指 string set。
//
// #@ sset.Create <字符串列表>
//    从字符串列表创建集合。
//
//    EXAMPLE: \@sset.Create{a;b;c}
//    RESULT:  {a;b;c}
//
//    EXAMPLE: \@sset.Create{h;e;l;l;o}
//    RESULT:  {h;e;l;o}
// };
#func {sset.Create} {
    #return {@sset.Add{{};%0}};
};

///=== {
// #@ sset.Size <集合>
//    计算集合的元素数量。
//
//    EXAMPLE: \@sset.Size{a;b;c}
//    RESULT:  3
//
//    EXAMPLE: \@sset.Size{@sset.Create{h;e;l;l;o}}
//    RESULT:  4
// };
#func {sset.Size} {
    #return {@slist.Size{%0}};
};

///=== {
// #@ sset.Contains <集合> <元素>
//    判断元素是否在集合中。
//
//    EXAMPLE:
//      #if {   \@sset.Contains{{a;b;c};{b}}    } {#echo OK1};
//      #if { ! \@sset.Contains{{a;b;c};{x}}    } {#echo OK2};
//      #if { ! \@sset.Contains{{};{x}}         } {#echo OK3};
//
//    OUTPUT:
//      OK1
//      OK2
//      OK3
// };
#func {sset.Contains} {
    #info arguments save;
    #local set  {$info[ARGUMENTS][1]};
    #local elem {$info[ARGUMENTS][2]};

    #if { @slist.IndexOf{{$set};{$elem}} > 0 } {
        #return 1;
    };
    #else {
        #return 0;
    };
};

///=== {
// #@ sset.Add <集合> <元素> [...]
//    将一个或者多个元素添加到集合当中。
//
//    EXAMPLE:
//      #local set {};
//      #local set {\@sset.Add{{$set}; a}};
//      #local set {\@sset.Add{{$set}; b; c}};
//      #nop 注意：因为是集合，所以重复元素不再添加;
//      #local set {\@sset.Add{{$set}; a; b; c}};
//      #local set {\@sset.Add{{$set}; x; y; z}};
//      #nop 注意：这里也有个重复的 l，只会被添加一次;
//      #local set {\@sset.Add{{$set}; h; e; l; l; o}};
//
//    ASSERT: {$set} === {a;b;c;x;y;z;h;e;l;o}
//
// } ;
#func {sset.Add} {
    #info arguments save;
    #unvar info[ARGUMENTS][0];
    #local argv {$info[ARGUMENTS]};

    #if { &argv[] < 2 } {
        #return {$argv[1]};
    };

    #local set  {$argv[1]};
    #return {@slist.FromList{@set.Add{{@list.FromSlist{$set}};$argv[2..-1]}}};
};

///=== {
// #@ sset.Remove <集合> <元素> [...]
//    从集合中删除一个或者多个元素。
//
//    EXAMPLE:
//      #local set {\@sset.Create{h;e;l;l;o;a;b;c;d}};
//      #local set {\@sset.Remove{{$set};a;b}};
//      #local set {\@sset.Remove{{$set};h;l;o}};
//      #local set {\@sset.Remove{{$set};x;y}};
//      #if { \@sset.Equal{{$set};{c;d;e}} } {
//          #echo OK;
//      };
//
//    OUTPUT: OK
// };
#func {sset.Remove} {
    #info arguments save;
    #unvar info[ARGUMENTS][0];
    #local argv {$info[ARGUMENTS]};

    #if { &argv[] < 2 } {
        #return {$argv[1]};
    };

    #local set  {$argv[1]};
    #return {@slist.FromList{@set.Remove{{@list.FromSlist{$set}};$argv[2..-1]}}};
};

///=== {
// #@ sset.Equal <集合1> <集合2>
//    判断集合1和集合2是否相等。
//    📖  如果两个集合中包含的元素是相同的，那么就判定为相等。相等判定并不要求元素顺序相同。
//
//    EXAMPLE:
//      #if { \@sset.Equal{{a;b;c};{a;b;c}} } {#echo OK1};
//      #if { \@sset.Equal{{x;y;z};{z;y;x}} } {#echo OK2};
//
//    OUTPUT:
//      OK1
//      OK2
// };
#func {sset.Equal} {
    #local set1  {%1};
    #local set2  {%2};

    #if { @sset.IsSubset{{$set1};{$set2}} && @slist.Size{$set1} == @slist.Size{$set2} } {
        #return 1;
    };
    #else {
        #return 0;
    };
};

///=== {
// #@ sset.IsSubset <集合1> <集合2>
//    判断集合2是否为集合1的子集。
//    📖  如果集合1包含了集合2中的每个元素，那么集合2就是集合1的子集。
//
//    EXAMPLE:
//      #if {   \@sset.IsSubset{{a;b;c;d;e};{a;c;e}}    } {#echo OK1};
//      #if { ! \@sset.IsSubset{{a;b;c;d;e};{a;b;X}}    } {#echo OK2};
//      #if {   \@sset.IsSubset{{a;b;c};{a;b;c}}        } {#echo OK3};
//      #if {   \@sset.IsSubset{{a;b;c};{}}             } {#echo OK4};
//      #if {   \@sset.IsSubset{{};{}}                  } {#echo OK5};
//
//    OUTPUT:
//      OK1
//      OK2
//      OK3
//      OK4
//      OK5
// };
#func {sset.IsSubset} {
    #local set1  {%1};
    #local set2  {%2};

    #local elem {0};
    #foreach {$set2} {elem} {
        #if { !@sset.Contains{{$set1};{$elem}} } {
            #return 0;
        };
    };

    #return 1;
};

///=== {
// #@ sset.IsProperSubset <集合1> <集合2>
//    判断集合2是否为集合1的真子集。
//    📖  如果集合1包含了集合中2的每个元素，而且集合1比集合2的元素还要多，
//       那么集合2就是集合1的真子集。
//
//    EXAMPLE:
//      #if {   \@sset.IsProperSubset{{a;b;c;d;e};{a;c;e}}  } {#echo OK1};
//      #if { ! \@sset.IsProperSubset{{a;b;c;d;e};{a;b;X}}  } {#echo OK2};
//      #if { ! \@sset.IsProperSubset{{a;b;c};{a;b;c}}      } {#echo OK3};
//      #if {   \@sset.IsProperSubset{{a;b;c};{}}           } {#echo OK4};
//      #if { ! \@sset.IsProperSubset{{};{}}                } {#echo OK5};
//
//    OUTPUT:
//      OK1
//      OK2
//      OK3
//      OK4
//      OK5
// };
#func {sset.IsProperSubset} {
    #local set1  {%1};
    #local set2  {%2};

    #if { @sset.IsSubset{{$set1};{$set2}} && @slist.Size{$set1} > @slist.Size{$set2} } {
        #return 1;
    };
    #else {
        #return 0;
    };
};

///=== {
// #@ sset.IsSuperset <集合1> <集合2>
//    判断集合2是否为集合1的超集。
//    📖  如果集合2包含了集合1中的每个元素，那么集合2就是集合1的超集。
//
//    EXAMPLE:
//      #if {   \@sset.IsSuperset{{a;c;e};{a;b;c;d;e}}  } {#echo OK1};
//      #if { ! \@sset.IsSuperset{{a;b;X};{a;b;c;d;e}}  } {#echo OK2};
//      #if {   \@sset.IsSuperset{{a;b;c};{a;b;c}}      } {#echo OK3};
//      #if {   \@sset.IsSuperset{{};{a;b;c}}           } {#echo OK4};
//      #if {   \@sset.IsSuperset{{};{}}                } {#echo OK5};
//
//    OUTPUT:
//      OK1
//      OK2
//      OK3
//      OK4
//      OK5
// };
#func {sset.IsSuperset} {
    #return {@sset.IsSubset{{%2};{%1}}};
};

///=== {
// #@ sset.IsProperSuperset <集合1> <集合2>
//    判断集合2是否为集合1的真超集。
//    📖  如果集合2包含了集合中1的每个元素，而且集合2比集合1的元素还要多，
//       那么集合2就是集合1的真超集。
//
//    EXAMPLE:
//      #if {   \@sset.IsProperSuperset{{a;c;e};{a;b;c;d;e}}    } {#echo OK1};
//      #if { ! \@sset.IsProperSuperset{{a;b;X};{a;b;c;d;e}}    } {#echo OK2};
//      #if { ! \@sset.IsProperSuperset{{a;b;c};{a;b;c}}        } {#echo OK3};
//      #if {   \@sset.IsProperSuperset{{};{a;b;c}}             } {#echo OK4};
//      #if { ! \@sset.IsProperSuperset{{};{}}                  } {#echo OK5};
//
//    OUTPUT:
//      OK1
//      OK2
//      OK3
//      OK4
//      OK5
// };
#func {sset.IsProperSuperset} {
    #return {@sset.IsProperSubset{{%2};{%1}}};
};

///=== {
// #@ sset.Union <集合1> <集合2> [...]
//    求两个或者多个集合的并集。
//    📖  并集是指把所有集合放在一块儿之后得到的新集合。
//
//    EXAMPLE: \@sset.Union{{a;b;c};{b;c;d};{c;d;e}}
//    RESULT: {a;b;c;d;e}
// };
#func {sset.Union} {
    #info arguments save;
    #unvar info[ARGUMENTS][0];
    #local argv {$info[ARGUMENTS]};

    #if { &argv[] < 2 } {
        #return {$argv[1]};
    };

    #local set  {@list.FromSlist{$argv[1]}};
    #local idx  {0};
    #loop {2} {&argv[]} {idx} {
        #local other {$argv[+$idx]};
        #local set {@set.Add{{$set};$other}};
    };

    #return {@slist.FromList{$set}};
};

///=== {
// #@ sset.Intersection <集合1> <集合2> [...]
//    求两个或者多个集合的交集。
//    📖  交集是指由同时出现在所有集合中的元素组成的新集合。
//
//    EXAMPLE: \@sset.Intersection{{a;b;c};{b;c;d};{c;d;e}}
//    RESULT:  {c}
// };
#func {sset.Intersection} {
    #info arguments save;
    #unvar info[ARGUMENTS][0];
    #local argv {$info[ARGUMENTS]};

    #if { &argv[] < 2 } {
        #return {};
    };

    #local set  {@list.FromSlist{$argv[1]}};
    #local idx  {0};
    #loop {2} {&argv[]} {idx} {
        #if { &set[] == 0 } {
            #return {};
        };

        #local other {$argv[+$idx]};
        #loop {&set[]} {1} {idx} {
            #local elem {$set[$idx]};
            #if { !@sset.Contains{{$other};{$elem}} } {
                #list set {delete} {$idx};
            };
        };
    };

    #return {@slist.FromList{$set}};
};

///=== {
// #@ sset.IsDisjoint <集合1> <集合2> [...]
//    判断两个或者多个集合是否为不交集。
//    📖  不交集是交集为空的意思。
//
//    EXAMPLE:
//      #if {   \@sset.IsDisjoint{{a;b;c};{b;c;d};{X;d;e}}  } {#echo OK1};
//      #if { ! \@sset.IsDisjoint{{a;b;c};{b;c;d};{c;d;e}}  } {#echo OK2};
//
//    OUTPUT:
//      OK1
//      OK2
// };
#func {sset.IsDisjoint} {
    #local set {@sset.Intersection{%0}};
    #if { @slist.Size{$set} == 0 } {
        #return 1;
    };
    #else {
        #return 0;
    };
};

///=== {
// #@ sset.Product <集合1> <集合2> [...]
//    计算两个或者多个集合的笛卡尔积。返回结果集合中的每个元素都是一个字符串，
//    其值由各个参数集合的元素拼接而成。
//    如果调用前设置了环境变量 sset-product-divider，则会以它为分隔符拼接。
//    <171>注意<299>，sset-product-divider 不能被设置为分号，这会导致灾难性的后果。
//    📖  两个集合的笛卡尔积是指由两个集合的元素组成的所有可能的有序对的集合。
//       多个集合的积也可由此扩展。
//
//    EXAMPLE:  \@sset.Product{{扬州;苏州};{当铺;钱庄}}
//    RESULT:   {扬州当铺;扬州钱庄;苏州当铺;苏州钱庄}
//
//    EXAMPLE:  \@sset.Product{{天;雷};{★ ;☆ };{凤凰;玄武}}
//    RESULT:   {天★ 凤凰;天★ 玄武;天☆ 凤凰;天☆ 玄武;雷★ 凤凰;雷★ 玄武;雷☆ 凤凰;雷☆ 玄武}
// };
#func {sset.Product} {
    #info arguments save;
    #unvar info[ARGUMENTS][0];
    #local argv {$info[ARGUMENTS]};

    #if { &argv[] < 2 } {
        #return {};
    };

    #local set  {@list.FromSlist{$argv[1]}};
    #if { &set[] == 0 } {
        #return {};
    };

    #local divider {@defaultVar{$sset-product-divider}};

    #local idx  {0};
    #loop {2} {&argv[]} {idx} {
        #local other {$argv[+$idx]};
        #if { "$other" == "" } {
            #return {};
        };

        #local newSet {};
        #local left {};
        #local right {};
        #loop {1} {&set[]} {left} {
            #local left {$set[+$left]};
            #foreach {$other} {right} {
                #list newSet {add} {{$left$divider$right}};
            };
        };

        #local set {$newSet};
    };

    #return {@slist.FromList{$set}};
};

///=== {
// #@ sset.Diff <集合1> <集合2> [...]
//    计算集合1与其它集合的相对差。
//    📖  相对差是指从集合1中去掉所有在其它集合中出现过的元素之后剩下的集合。
//
//    EXAMPLE:  \@sset.Diff{{a;b;c;d;e};{c;d;e;f;g}}
//    RESULT:   {a;b}
//
//    EXAMPLE:  \@sset.Diff{{a;b;c;d;e;f};{a;c};{d;g};{f;x}}
//    RESULT:   {b;e}
// };
#func {sset.Diff} {
    #info arguments save;
    #unvar info[ARGUMENTS][0];
    #local argv {$info[ARGUMENTS]};

    #if { &argv[] < 2 } {
        #return {$argv[1]};
    };

    #local set  {@list.FromSlist{$argv[1]}};
    #local idx  {0};
    #loop {2} {&argv[]} {idx} {
        #if { &set[] == 0 } {
            #return {};
        };

        #local other {$argv[+$idx]};
        #local set {@set.Remove{{$set};$other}};
    };

    #return {@slist.FromList{$set}};
};

///=== {
// #@ sset.SymmDiff <集合1> <集合2>
//    计算集合1与集合2的对称差。
//    📖  对称差是指并集与交集的相对差。
//
//    EXAMPLE:  \@sset.SymmDiff{{a;b;c;d;e};{c;d;e;f;g}}
//    RESULT:   {a;b;f;g}
// };
#func {sset.SymmDiff} {
    #return {@sset.Diff{{@sset.Union{{%1};{%2}}};{@sset.Intersection{{%1};{%2}}}}};
};
