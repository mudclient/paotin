#alias N {n;nd;nu};
#alias E {e;ed;eu};
#alias W {w;wd;wu};
#alias S {s;sd;su};
#alias SW {sw;S;W};
#alias SE {se;S;E};
#alias NE {ne;N;E};
#alias NW {nw;N;W};

#alias {beep} {#echo {\a\}};

#event {TIME 08:55} {#echo <129>准备保卫！！！<299>};
#event {TIME 15:55} {#echo <129>准备保卫！！！<299>};
#event {TIME 20:25} {#echo <129>准备保卫！！！<299>};
#event {TIME 20:55} {#echo <129>准备保卫！！！<299>};

#event {TIME 07:55} {#echo <129>准备答题！！！<299>};
#event {TIME 19:55} {#echo <129>准备答题！！！<299>};

#alias {bwcx} {
    #local keyword {%1};
    #if { "$keyword" == "" && "$char[档案][门派]" != "" } {
        #local keyword {$char[档案][门派]};
    };

    #class bwcx open;
    #high {^天下争霸第%*$keyword%*$} {light magenta};
    #action {^开始后，自动安排好三天后的比武。$} {bwcx.return};
    #action {^等等，系统喘气中......$}           {bwcx.retry};
    #alias {bwcx.retry}     {#delay bwcx.retry {bwcx} 5};
    #alias {bwcx.return}    {#class bwcx kill};
    #class bwcx close;

    xtt.Send bwcx;
};

#alias chat-ren-song {
    chat.TuneOn chat;
    chat* admit $user[id];
    chat* xuanwu;
    chat* sob $user[id];
    chat* cry $user[id];
    chat* cry;
    chat* weak;
};

#alias chat-shuang {
    chat.TuneOn chat;
    chat* massage $user[id];
    chat* shuang;
    chat* shuang $user[id];
    chat* shuang;
};

#alias chat-zb {
    chat.TuneOn chat;
    chat* unme $user[id];
    chat* unme;
    chat* mark $user[id];
    chat* mark;
    chat* bud $user[id];
};

#alias chat-sz {
    chat.TuneOn chat;
    chat $user[name]动作妩媚，朝你微微一笑,使出了「美人三招」！;
    chat $user[name]使完「贵妃回眸」，想起英雄三招，意犹未尽，又出三招！;
    chat $user[name]使完「小怜横陈」，想起自己历经劫难已是武学大家，不由得豪气干云，又出三招！;
    chat $user[name]使完「飞燕回翔」，想起多年召蛇心得，心血来潮，自创三招！;
    chat $user[name]一共向你使出了一十八招！;
};

load-lib sync;

#alias {i-all} {
    #class i-all open;
    #action {^你身上id为%*的东西有下面这些：{|ID=i-all}$}   {#line gag};
    #action {^你身上没有id为%*的东西。{|ID=i-all}$}         {#line gag};
    #action {^[命令进入缓冲]{|ID=i-all}$}                   {#line gag};
    #action {^{>|}%s{|ID=i-all}$}                           {#line gag};
    #class i-all close;

    i sword;
    i blade;
    i dagger;
    i staff;
    i needle;
    i flute;
    i whip;
    i axe;
    i spear;
    i hammer;
    i halberd;
    i hook;

    i ring;
    i necklace;

    i armor;
    i cloth;
    i boots;
    i shield;
    i surcoat;
    i wrists;
    i hands;
    i head;
    i waist;

    i armet;
    i bracer;
    i cape;
    i gauntlet;
    i belt;
    i mask;
    i cuirass;
    i pauldron;
    i cuisse;

    sync.Wait {#class i-all kill};
};

#alias {ii} {
    #class i-all open;
    #action {^(%s%d)  (绑)} {#line gag};
    #class i-all close;
    i-all;
};

#alias {pp} {chat.TuneOn helpme; helpme; helpme find %0};

#alias {cgem} {do 3 pickgem %1; combine gem; pack gem};
#alias jd jianding;

#alias tichun {
    #class tichun open;
    #action {^你目前的修为还可以再转换%%1点内力变为真气。$E} {#delay 0 {xtt.Send tichun %%1}} {9.500};
    #class tichun close;
    #delay 1 {xtt.Send tichun $char[HP][当前内力]};
};

#gag {向红豆打听有关『%*』的消息。$E};

#gag {^%s$};
#action {^>%s$} {#line gag};
#gag {^├{(─|┼)+}┤$};

#action {^%*给你一枚%*{☆|★|◎}%*。$E} {
    pack gem;
} {9.999};

#alias kk {look %0; lookin %0; follow %0; killall %0};
#action {^你瞬间感到了危险临近，奸细就在附近！$E} {killall} {9.500};

#action {^一支铜雀轻轻地落在你的面前，你可以选择下列任务系列中一组：$E} {
    xtt.DisableAllCommand;
} {9.999};

#action {^你一顿棍棒乱揍，看来杀手有点怕你了。$E}                   {halt;quan} {9.500};
#action {^( 蒙面杀手似乎十分疲惫，看来需要好好休息了。)$E}          {halt;ask killer about 认输} {9.999};
#action {^( 蒙面杀手已经陷入半昏迷状态，随时都可能摔倒晕去。)$E}    {halt;ask killer about 认输} {9.999};

#action {^你要进入哪间储物柜？$E} {
    #line gag;
    enter $user[id];
    find;
} {9.500};

#action {^你仔细地将鹿皮袋绑在腰间。$E} {put shi zi in lupi dai} {9.500};
#action {^你捡起一把玉石子。$E}         {put shi zi in lupi dai} {9.500};
#action {^你从%*搜出一把玉石子。$E}     {put shi zi in lupi dai} {9.500};

#action {^没有经过通传，任何人等不得擅闯都统治府。$E} {ask shiwei about 通传} {9.999};
#action {^你因为种种原因停了下来，可以用walk继续进行。$E} {#delay 1 xtt.Send walk} {9.500};
#action {^艄公说“到啦，上岸吧”，随即把一块踏脚板搭上堤岸。$E} {out} {9.500};
#action {^大车停稳了下来，你可以下车(xia)了。$} {xia} {9.500};

#action {^任务已经失败，离开请输入(leave %*)。$} {leave %1} {9.500};
#action {^任务完成，想要离开请输入(leave %*)。$} {leave %1} {9.500};

#action {^你掉转船头，迅速的划回到了鄱阳湖边。$E} {
    give xiang to ren;
} {9.500};

#action {^临走之际，你忽然发现一个盒子看起来有些特殊！$E} {
    open box;
} {9.999};

#subst {~\e[34m} {\e[94m};

VAR 服务器重启时间 gServerRebootTime {0};

#action {^北大侠客行再过%*将重新启动 ...$} {
    #local time {@time.ParseDoC{%1}};
    #var gServerRebootTime {@time.Now{} + $time};
    prompt.Set {{reboot}{$time}};
};

#alias {list-armors} {
    #class rbz-list-armors open;

    #action {^┏所有防具列表┓$} {
        #class rbz-list-armors open;

        #gag {^│%d%s│%*│1%s│$};

        #action {^│%d%s│%*(%*)%*│2%s│$} {
            #if { "%%%4" != "{ring|necklace}" } {
                #line gag;
            };
        };

        #high {^%*({ring|necklace})%*$} {light magenta};

        #action {^└─────┴────────────────────┴──┴──┴───┘$} {
            #class rbz-list-armors kill;
            #buffer end;
        };

        #class rbz-list-armors close;
    };

    #action {^你不是刚看过么？$} {
        #class rbz-list-armors kill;
    };

    #class rbz-list-armors close;

    list armors;
};

#action {^你向当铺买下一件%*之%*。$} {
    #switch {"%2"} {
        #case {"盔"}    {jianding head};
        #case {"护腕"}  {jianding wrists};
        #case {"披风"}  {jianding surcoat};
        #case {"手套"}  {jianding hands};
        #case {"铠甲"}  {jianding armor};
        #case {"袍"}    {jianding cloth};
        #case {"腰带"}  {jianding waist};
        #case {"盾"}    {jianding shield};
        #case {"靴"}    {jianding boots};
        #case {"项链"}  {jianding necklace};
        #case {"戒指"}  {jianding ring};
        #case {"剑"}    {jianding sword};
        #case {"刀"}    {jianding blade};
        #case {"杖"}    {jianding staff};
        #case {"鞭"}    {jianding whip};
        #case {"斧"}    {jianding axe};
        #case {"枪"}    {jianding spear};
        #case {"锤"}    {jianding hammer};
        #case {"戟"}    {jianding halberd};
        #case {"匕"}    {jianding dagger};
        #case {"针"}    {jianding needle};
        #case {"钩"}    {jianding hook};
        #case {"箫"}    {jianding flute};
        #default {#0};
    };
};

#alias {%S_%S} {node walk %0} 9;

load-lib sync;

#alias {yz_zj} {
    walk zhenjiang;
    sync.Wait {map.GuoJiang zhenjiang};
};

#alias {zj_yz} {
    walk yangzhou;
    sync.Wait {map.GuoJiang yangzhou};
};

#alias {map.GuoJiang} {
    #local target {%1};

    #line sub var #line oneshot #action {^一叶扁舟缓缓地驶了过来，艄公将一块踏脚板搭上堤岸，以便乘客上下。$} {
        #untick map.yell;
        #line oneshot #action {^艄公说“到啦，上岸吧”，随即把一块踏脚板搭上堤岸。$} {
            out;
            walk $target;
        } {4};
        enter;
    } {4};

    xtt.Tick map.yell {yell boat} 1;
};
