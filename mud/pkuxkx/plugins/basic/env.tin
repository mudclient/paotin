#nop vim: set filetype=tt:;

/*
本文件属于 PaoTin++ 的一部分
===========
PaoTin++ © 2020~2024 的所有版权均由担子炮(dzp <danzipao@gmail.com>) 享有并保留一切法律权利
你可以在遵照 GPLv3 协议的基础之上使用、修改及重新分发本程序。
===========
*/

#var basic_env[META] {
    {NAME}      {系统环境}
    {DESC}      {维护服务器环境，包括环境变量和游戏设定}
    {AUTHOR}    {担子炮}
    {NOTE}      {本文件属于 PaoTin++ 的一部分}
};

VAR {服务器环境变量}    {env.Var}           {};
VAR {当前游戏周}        {env.Week}          {};
VAR {服务器重启时间}    {env.RebootTime}    {0};

event.Define {env/set}          {无参} {$MODULE} {环境变量发生改变(set/unset)时发送本事件，本事件只会唤醒与变量名相匹配的钩子};
event.Define {env/update}       {无参} {$MODULE} {环境变量列表已更新};
event.Define {env/time/update}  {无参} {$MODULE} {当前游戏周的内容已更新};

#alias {env.parse} {
    #class env.parse open;

    #var env.parse.old {$env.Var};
    #var env.Var {};

    #action {~^%c\e[44;1m%S %s %c \e[1;37m%*%s%+1c$} {
        #local key {%%2};
        #local value {%%5};
        #replace value {^"%*"$} {&1};
        #var {env.Var[$key]} {$value};
        #if { "$env.parse.old[$key]" !== "$value" } {
            event.Emit {env/set} {env/$key};
        };
    };

    #action {^│变量%s│数值%s│$} {#0} {4};

    #action {^│%s%S%s│%s%*%s│$} {
        #local key {%%2};
        #local value {%%5};
        #var {env.Var[$key]} {$value};
        #if { "$env.parse.old[$key]" !== "$value" } {
            event.Emit {env/set} {env/$key};
        };
    };

    event.HandleOnce GA {basic/env} {basic/env} {
        event.Emit env/update;
        okLog 环境变量已更新。;
        #class env.parse kill;
        #0
    };

    #class env.parse close;
};

///=== {
// ## env.Refresh
//    向服务器发送 set 命令，以图刷新本地变量的值。
// };
#alias {env.Refresh} {
    #class env.Refresh open;

    #gag {^%*{|ID=basic/env}$};

    #alias {env.Refresh.done} {
        #class env.Refresh kill;
    };

    event.ClassHandleOnce {env/update} {env.Refresh} {basic/env} {env.Refresh.done};
    sync.ClassWait {env.Refresh.done};

    #class env.Refresh close;

    set;
};

///=== {
// #@ env.Get <变量名>
//    返回该环境变量的当前值。
// };
#func {env.Get} {
    #local key {%1};

    #return {$env.Var[$key]};
};

///=== {
// #@ env.Set <变量名> <变量值> [<是否强制发送命令>]
//    设置环境变量。
//    如果新值与旧值相同，那么不会真的发送命令，除非第三个参数设置为 true。
// };
#alias {env.Set} {
    #local key      {%1};
    #local value    {%2};
    #local force    {@default{{%3};false}};

    #local old {$env.Var[$key]};
    #if { "$old" === "$value" } {
        #if { @isFalse{$force} } {
            dbgLog env => 环境变量 $key 的已经被设为 $value，因此不再向服务器发送命令。;
            #return;
        };
    };

    set $key $value;
};

///=== {
// #@ env.UnSet <变量名> [<是否强制发送命令>]
//    删除环境变量。
//    如果环境变量本不存在，那么不会真的发送命令，除非第二个参数设置为 true。
// };
#alias {env.UnSet} {
    #local key      {%1};
    #local force    {@default{{%2};false}};

    #local old {$env.Var[$key]};
    #if { "$old" == "" } {
        #if { @isFalse{$force} } {
            dbgLog env => 环境变量 $key 已经取消，因此不再向服务器发送命令。;
            #return;
        };
    };

    unset $key;
};

#action {^取消环境变量：%*$E} {
    #local name {%1};
    #local changed {0};

    #if { "$env.Var[$name]" != "" } {
        #local changed 1;
    };

    #unvar {env.Var[$name]};

    #if { $changed } {
        event.Emit {env/set} {env/$name};
        okLog 环境变量已更新。;
    };
};

#action {^设定环境变量：%S = %*$E} {
    #local key {%1};
    #local value {%2};

    #local old {$env.Var[$key]};

    #replace value {^"%*"$} {&1};
    #var {env.Var[$key]} {$value};

    #if { {$old} !== {$value} } {
        event.Emit {env/set} {env/$key};
        okLog 环境变量已更新。;
    };
};

#action {@re.TableHeader{设定变量集}} {
    env.parse;
};

#action {^北大侠客行再过%*将重新启动 ...$} {
    #local time {@time.ParseDoC{%1}};
    #var env.RebootTime {@time.Now{} + $time};
    prompt.Set {{reboot}{$time}};
};

/*
╭───玄武之周┬──────────────────────────────────╮
│    仆人      │玄武职业防御提升。                                                  │
│    ▂▂    慕│剩余时间：二小时二十二分二十二秒。                                  │
│    o  o      │                                                                    │
│   ▂〇▂ ▌容│↑心上人、香闺怨、天珠及见性成佛、纪晓芙、万安塔、破解武功、韩世忠。│
│ ▅      █   │→破阵、青城任务、韩员外、都府刺杀、护镖、公孙止。                  │
│ █      ▄ 仆│↓萧峰、偷学、谍报、暗杀、胡一刀、苗疆、宋远桥。                    │
│ ▌           │                                                                    │
│            人│北京时间2024年4月6日 10时17分49秒，重启后约五天二十三小时四十六分钟 │
│ ▄▄  ▄▄   │☆侠纪年☆一六八二年八月十六日傍晚时分                              │
╰───────┴─────────────────────────北大侠客行────╯
*/
#action {@re.TableHeader{%*之周}} {
    #var env.Week[name] {%1之周};

    #class env.Time.parse open;

    #action {^│%*│剩余时间：%*。%s│{|ID=env/Time/parse}$} {
        #var env.Week[remaining] {@time.ParseDoC{%%2}};
        prompt.Set {{week}{$env.Week[name]($env.Week[remaining])}};
    };

    #action {^│%*│{↑|→|↓}%*│{|ID=env/Time/parse}$} {
        #local jobs {%%3};
        #replace jobs {。%*} {};
        #replace jobs {、} {;};
        #replace jobs {及} {;};

        #switch {"%%2"} {
            #case {"↑"} {#var env.Week[buff]   {$jobs}};
            #case {"→"} {#var env.Week[normal] {$jobs}};
            #case {"↓"} {#var env.Week[debuff] {$jobs}};
        };
    };

    #action {^│%*│北京时间%d年%d月%d日%s%d时%d分%d秒，将于%d月%d日%s%d时%d分重启。%s│$} {
        #nop 简单处理一下，假设重启预报不会超过 24 小时。;
        #local reboot {@math.Eval{(%%12 - %%6) * 3600 + (%%13 - %%7) * 60 + 60 - %%8}};
        #if { %%10 > %%4 } {
            #local reboot {@math.Eval{(23 - %%6) * 3600 + (59 - %%7) * 60 + 60 - %%8}};
            #local reboot {@math.Eval{$reboot + %%12 * 3600 + %%13 * 60}};
        };
        #local env.RebootTime {@time.Now{} + $reboot};
        prompt.Set {{reboot}{$reboot}};
    };

    event.HandleOnce GA {basic/env} {basic/env} {
        event.Emit env/time/update;
        okLog 当前游戏周已更新。;
        #class env.Time.parse kill;
        #0
    };

    #class env.Time.parse close;
};

#alias {env.Time} {
    #class env.Time open;

    #gag {^%*{|ID=env/Time}$};

    #alias {env.Time.done} {
        #class env.Time kill;
    };

    time;

    event.ClassHandleOnce {env/time/update} {env.Time} {basic/env} {env.Time.done};
    sync.ClassWait {env.Time.done};

    #class env.Time close;
};

#action {^【江湖】%*等任务奖励下降，%*等任务奖励不变，%*等任务奖励上升，%*奖励最佳。$E} {
    #line gag;
    jhLog %99;
} {4.000};

#action {^【江湖】经推算，本周为%*，%*！$E} {
    #line gag;
    jhLog %99;
    env.Time;
} {4.000};

VAR {通缉信息} {env.Wanted}     {};

#action {^你因为杀害%*，将被%*通缉%*。$E} {
    #var env.Wanted[reason]     {%1};
    #var env.Wanted[city]       {%2};

    #local duration {@time.ParseDoC{%3}};
    prompt.Set {{wanted} {<119>%2<299>($duration)}};
    #var env.Wanted[deadline]   {@math.Eval{@time.Now{} + $duration}};
};
