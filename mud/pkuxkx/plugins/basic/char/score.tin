#nop vim: set filetype=tt:;

/*
本文件属于 PaoTin++ 的一部分
===========
PaoTin++ © 2020~2023 的所有版权均由担子炮(dzp <danzipao@gmail.com>) 享有并保留一切法律权利
你可以在遵照 GPLv3 协议的基础之上使用、修改及重新分发本程序。
===========
*/

#var basic_char_score[META] {
    {NAME}      {角色档案}
    {DESC}      {解析 score 命令，并储存到变量 char[档案]}
    {AUTHOR}    {担子炮}
    {NOTE}      {本文件属于 PaoTin++ 的一部分}
};

load-module {basic/title};
load-lib event;

event.Define {char/score} {无参} {$MODULE} {score 命令的数据解析成功时，发送本事件}

/*
╭───人物详情────────────────────────┬───────╮
│ 武当派真人 马后炮(Mhp)                                       │ ▂▃▅▃▂   │
│ 膂力：[  69]  悟性：[  47]  根骨：[  83]  身法：[  79]       │    -  -      │
│ 福缘：[  23]  容貌：[  39]  灵性：[  39]  胆识：[   ?]       │   ▂  ▂     │
│ 距离上榜还需增加206218点经验。                               │     32/32    │
├───────────────────┬───────────┴───────┤
│个人信息                              │门派履历                              │
│国籍：无国籍                          │门派：武当派                          │
│年龄：三十四岁                        │师承：张三丰                          │
│性别：男性                            │门忠：7195                            │
│生日：己亥年一月三十一日申时七刻      │出师：无                              │
│婚姻：未婚                            │叛师：无                              │
├────────────┬──────┴─────┬─────────────┤
│杀生：0人               │职业：甲士              │存款：527金条 21713黄金   │
│被杀：1次               │道德：100.88万          │武学点：0                 │
│死亡：4次               │声望：322.77万          │国家积分：0               │
│杀气：正常              │愿望：150               │实战经验：一甲子余年      │
├───休闲任务─────┼────────────┼─────────────┤
│高级任务：0次           │低级任务：6次           │劫匪掉宝：0次             │
╰────────────┴────────────┴────北大侠客行────╯
*/

#var char-menpai-id {
    {少林派}    {slp}
    {武当派}    {wdp}
    {峨嵋派}    {emp}
    {华山派}    {hsp}
    {星宿派}    {xxp}
    {神龙教}    {slj}
    {白驼山}    {bts}
    {灵鹫宫}    {ljg}
    {丐帮}      {gb}
    {全真派}    {qzp}
    {古墓派}    {gmp}
    {天龙寺}    {tls}
    {朝廷}      {ct}
    {天地会}    {tdh}
    {桃花岛}    {thd}
    {雪山派}    {xsp}
    {明教}      {mj}
    {日月神教}  {rysj}
    {姑苏慕容}  {gsmr}
    {大轮寺}    {dls}
    {绝情谷}    {jqg}
    {五毒教}    {wdj}
};

#action {^╭───人物详情────────────────────────┬───────╮$} {
    #class char-score-parser open;

    #var char-score-parser-lineNo {0};
    #action {^│%*│%*│$} {
        #math char-score-parser-lineNo {${char-score-parser-lineNo} + 1};
        #switch {"${char-score-parser-lineNo}"} {
            #case {"1"} {
                #local obj {@ParseTitle{@str.Trim{%%1}}};
                #var char[档案][头衔] {$obj[title]};
                #var char[档案][大名] {$obj[name]};
                #var char[档案][账号] {$obj[id]};
            };
            #case {"2"} {
                #local level {@str.Trim{%%2}};
                #replace level {%d/%d} {{cur}{&1}{max}{&2}};
                #local level {$level};
                #var char[档案][人物等级] {$level[cur]};
                #var char[档案][最大等级] {$level[max]};
            };
            #default {
                #nop;
            };
        };
    } {6};

#nop │ 膂力：[  69]  悟性：[  47]  根骨：[  83]  身法：[  79]       │    -  -      │;
    #action {~^\e[0m│\e[2;37;0m 膂力：[%+]  悟性：[%+]  根骨：[%+]  身法：[%+]%+│%+│} {
        #local str {%%1};
        #local int {%%2};
        #local con {%%3};
        #local dex {%%4};

        #local key {先天};
        #if { "$str$int$con$dex" == "%*[1;33m%*" } {
            #local key {天赋};
        };
        #elseif { "$str$int$con$dex" == "%*[36m%*" } {
            #local key {初始};
        };

        #replace str {{\x1b\[[0-9;\\]+m}} {};
        #replace int {{\x1b\[[0-9;\\]+m}} {};
        #replace con {{\x1b\[[0-9;\\]+m}} {};
        #replace dex {{\x1b\[[0-9;\\]+m}} {};

        #var char[档案][$key][膂力] {@str.Trim{$str}};
        #var char[档案][$key][悟性] {@str.Trim{$int}};
        #var char[档案][$key][根骨] {@str.Trim{$con}};
        #var char[档案][$key][身法] {@str.Trim{$dex}};
    };
#nop │ 福缘：[  23]  容貌：[  39]  灵性：[  39]  胆识：[   ?]       │   ▂  ▂     │;
    #action {^│ 福缘：[%+]  容貌：[%+]  灵性：[%+]  胆识：[%+]%*│%*│$} {
        #var char[档案][天赋][福缘] {@str.Trim{%%1}};
        #var char[档案][天赋][容貌] {@str.Trim{%%2}};
        #var char[档案][天赋][灵性] {@str.Trim{%%3}};
    };

#nop │国籍：大理        性别：男性          │门派：神龙教                          │;
#nop │身高：七尺六寸    体重：约一百一十斤  │师承：洪安通                          │;
#nop │体型：匀称        姻缘：未遇良人      │门忠：2940                            │;
    #action {^│%+：%* %+：%+│%+：%*│$} {
        #var char[档案][%%1] {@str.Trim{%%2}};
        #var char[档案][@str.Trim{%%3}] {@str.Trim{%%4}};
        #var char[档案][%%5] {@str.Trim{%%6}};
    } {5.1};

#nop │年龄：十八岁二个月                    │出师：无                              │;
#nop │生日：辛丑年十二月五日亥时八刻        │叛师：无                              │;
    #action {^│%+：%*│%+：%*│$} {
        #local age {@str.Trim{%%2}};
        #replace age {%*岁{|(.*)个月}$} {@math.Eval{@math.ParseCN{&1} + @math.Eval{@math.ParseCN{@default{&3;0}} * 1.0 / 12}}};
        #var char[档案][%%1] {$age};
        #var char[档案][%%3] {@str.Trim{%%4}};
    } {5.2};

#nop │杀生：0人               │职业：甲士              │存款：527金条 21713黄金   │;
#nop │被杀：1次               │道德：100.88万          │武学点：0                 │;
#nop │死亡：4次               │声望：322.77万          │国家积分：0               │;
#nop │杀气：正常              │愿望：150               │实战经验：一甲子余年      │;
#nop ├───休闲任务─────┼────────────┼─────────────┤;
#nop │高级任务：0次           │低级任务：6次           │劫匪掉宝：0次             │;
    #action {^│%+：%*│%+：%*│%+：%*│$} {
        #var char[档案][%%1] {@str.Trim{%%2}};
        #var char[档案][%%3] {@str.Trim{%%4}};
        #var char[档案][%%5] {@str.Trim{%%6}};
    };

    #nop 白驼山    你是白驼山第二代弟子，师承欧阳锋。;
    #nop 星宿派    你是星宿派第三代弟子，师承摘星子。;
    #nop 古墓派    你是古墓派第四代弟子，师承杨过。;
    #nop 天龙寺    你是天龙寺第十五代弟子，师承枯荣大师。;
    #nop 大轮寺    你是大轮寺第十一代弟子，师承鸠摩智。;
    #nop 峨嵋派    你是峨嵋派第四代弟子，师承灭绝师太。;
    #nop 丐帮      你是丐帮第十八代弟子，师承洪七公。;
    #nop 华山派    你是华山派第十八代弟子，师承风清扬。;
    #nop 灵鹫宫    你是灵鹫宫第二代弟子，师承天山童姥。;
    #nop 雪山派    你是雪山派第六代弟子，师承白自在。;
    #nop 明教      你是明教第二十二代弟子，师承颜垣。;
    #nop 姑苏慕容  你是姑苏慕容第二代弟子，师承慕容博。;
    #nop 全真派    你是全真派第四代弟子，师承尹志平。;
    #nop 日月神教  你是日月神教第二十代弟子，师承东方不败。;
    #nop 少林派    你是少林派第三十七代弟子，师承玄悲大师。;
    #nop 神龙教    你是神龙教第二代弟子，师承洪安通。;
    #nop 桃花岛    你是桃花岛第二代弟子，师承黄药师。;
    #nop 天地会    你是天地会第四代弟子，师承徐天川。;
    #nop 武当派    你是武当派第二代弟子，师承张三丰。;
    #nop 五毒教    你是五毒教第十代弟子，师承齐云琳。;
    #action {^ 你是%*第%*代弟子，师承%*。$} {
        #var char[档案][门派]   {%%1};
        #var char[档案][门派ID] {$char-menpai-id[%%1]};
        #var char[档案][辈分]   {@math.ParseCN{%%2}};
        #var char[档案][师父]   {%%3};
    };

    #nop 朝廷      你是朝廷四品官员，师承孟珙。;
    #action {^ 你是朝廷%*品官员，师承%*。$} {
        #var char[档案][门派]   {朝廷};
        #var char[档案][门派ID] {$char-menpai-id[朝廷]};
        #var char[档案][辈分]   {@math.ParseCN{%%1}};
        #var char[档案][师父]   {%%2};
    };

    #action {^╰────────────┴────────────┴────%S────╯{|ID=char.score}$} {
        #class char-score-parser kill;
        event.Emit {char/score};
    };

    #class char-score-parser close;
};

event.HandleOnce {user-online} {score/init} {basic/char} {
    score;
};
