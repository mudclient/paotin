#nop 角色生命值信息解析模块;

load-lib event;

event.Define {char/status}    {无参}  {$MODULE} {已经获取到 status_me 命令输出结果，并更新 char[STATUS]。};

#nop ╭────个人状态────┬────┬──────┬────┬──────┬────┬─────╮;
#nop │姓名    │马后炮      │ＩＤ    │Mhp         │级别    │32          │战斗力  │104611      │;
#nop ├───攻击属性────┼────┼──────┼────┼──────┼────┼──────┤;
#nop │武器伤害│264         │出招速度│0.58        │外伤命中│806         │内伤命中│-2级        │;
#nop │外伤增益│15%         │内伤增益│6%          │要害攻击│2%          │要害伤害│152%        │;
#nop │毒伤增强│0%          │致晕命中│0级         │        │            │        │            │;
#nop ├───防御属性────┼────┼──────┼────┼──────┼────┼──────┤;
#nop │护甲    │597         │防御    │382         │躲闪    │818         │招架    │2846        │;
#nop │外伤抗性│50%         │外伤绝抗│0%          │内伤抗性│45%         │内伤绝抗│0%          │;
#nop │真气减伤│9%          │伤害吸收│0%          │外伤减伤│0           │技能防御│4级         │;
#nop │要害抗性│1%          │毒伤抗性│50%         │致晕抗性│+3级        │        │            │;
#nop ├───其他属性────┼────┼──────┼────┼──────┼────┼──────┤;
#nop │臂力影响│54          │悟性影响│45          │根骨影响│75          │身法影响│59          │;
#nop │后天身法│10点        │        │            │        │            │        │            │;
#nop ├───技能加成────┼────┼──────┼────┼──────┼────┼──────┤;
#nop │基本轻功│+57级       │基本招架│+40级       │基本拳法│+25级       │基本爪法│+25级       │;
#nop │基本手法│+24级       │基本剑法│+50级       │基本刀法│+50级       │        │            │;
#nop ├───恢复状态────┼────┼──────┼────┼──────┼────┼──────┤;
#nop │气血基础│3381        │等级加成│1589        │高等加成│0           │其他加成│18050       │;
#nop │精神基础│1964        │等级加成│926         │高等加成│0           │其他加成│2882        │;
#nop │气血恢复│309.37      │气血治疗│49.79       │精神恢复│389.58      │精神治疗│49.79       │;
#nop │内力恢复│43.05       │精力恢复│14.32       │内力精纯│100.00%     │加力减耗│0%          │;
#nop ├───战斗信息────┴────┴──────┴────┴──────┴────┴──────┤;
#nop │你使用「太极剑法」中的绝招「太极剑·连」攻击对手：喻永思(yu yongsi)，命中(986)，对方躲闪(1509 │;
#nop │)，招架(1795)，最终命中概率12.00%。                                                           │;
#nop ├───特殊状态────────────────────────────────────────┤;
#nop │身轻如燕永久有效，躲闪几率增加10%。                                                           │;
#nop │太极神功永久有效，最大气血 + 5100点。                                                         │;
#nop │铜皮铁骨永久有效，防御躲闪 + 117级，护甲防御 + 235点。                                        │;
#nop ├───健康状态────────────────────────────────────────┤;
#nop │受伤、怒、梯云纵(127秒)、太极剑·随(11秒)、太极剑·连(4秒)、太极剑·缠(1秒)                   │;
#nop ├───奖励状态────┬────┬──────┬────┬──────┬────┬──────┤;
#nop │幸运值  │25          │奖励系数│115%        │识图检查│完整奖励    │        │            │;
#nop ╰────┴──────┴────┴──────┴────┴──────┴──北大侠客行────╯;

#alias {char.status_me.parse.output} {
    #local silent {%1};

    #class char-parse-status-me open;

    #if { "$silent" == "gag" } {
        #gag {^%*$};
    };

#nop │气血恢复│6.42    │气血治疗│0.11    │精神恢复│3.16    │精神治疗│0.11    │;
    #action {^│气血恢复│%S%!s│气血治疗│%S%!s│精神恢复│%S%!s│精神治疗│%S%!s│$} {
        #var char[STATUS][气血恢复] {@trim{%%1}};
        #var char[STATUS][气血治疗] {@trim{%%2}};
        #var char[STATUS][精神恢复] {@trim{%%3}};
        #var char[STATUS][精神治疗] {@trim{%%4}};
    };

#nop │内力恢复│4.11    │精力恢复│12.63   │内力精纯│97.69%  │加力减耗│0%      │;
    #action {^│内力恢复│%S%!s│精力恢复│%S%!s│内力精纯│%S\%%!s│加力减耗│%*│$} {
        #var char[STATUS][内力恢复] {@trim{%%1}};
        #var char[STATUS][精力恢复] {@trim{%%2}};
        #var char[STATUS][内力精纯] {@trim{%%3}};
    };

#nop 兜底儿的触发，捕获其它所有数值类型字段;
    #action {^│%*│%S%!s│%*│%S%!s│%*│%S%!s│%*│%S%!s│$} {
        #local data {
            {%%1}   {%%2}
            {%%3}   {%%4}
            {%%5}   {%%6}
            {%%7}   {%%8}
        };

        #local key {};
        #foreach {*data[]} {key} {
            #local value {@trim{$data[$key]}};
            #local key   {@trim{$key}};
            #if { "$key" != "" } {
                #replace value {{[^0-9.-]+}} {};
                #var char[STATUS][$key] {$value};
            };
        };
    } {5.1};

#nop ├───健康状态────────────────────────────────────────┤;
#nop │ 受伤、怒、梯云纵(127秒)、太极剑· 随(11秒)、太极剑· 连(4秒)、太极剑· 缠(1秒)                   │;
    #action {^├───健康状态──{(┴|─)+}───┤$} {
        #line oneshot #action {~^\e[0m│%*\e[2;37;0m{?:|、\x1b\[1;36m(.*)\x1b\[2;37;0m}%s\e[2;37;0m\e[2;37;0m│\e[0m$} {
            #var char[STATUS][健康状态] {%%%1};
            #var char[STATUS][持续效果] {%%%2};
        };
    };

#nop 　持续效果提示    　梯云纵(87秒)、太极剑· 随(37秒)                                      　;
    #action {^　持续效果提示    　%*　$} {
        #local status {@trim{%%1}};
    };

#nop │幸运值      │2           │奖励系数    │100%        │识图检查    │需要    │;
    #action {^│幸运值%!s│%d%!s│奖励系数%!s│%d%!.%!s│识图检查%!s│%*│$} {
        #var char[STATUS][幸运值]   {@trim{%%1}};
        #var char[STATUS][奖励系数] {@trim{%%2}};
        #var char[STATUS][识图检查] {@trim{%%3}};
    };

#nop ╰────┴──────┴────┴──────┴────┴──────┴──北大侠客行────╯;
    #line oneshot #action {^╰{(─|┴)+}──%S────╯{|ID=char.status}$} {
        #if { "%1" == "gag" } {#line gag};
        char.status_me.parse.done;
    };

    #alias {char.status_me.parse.done} {
        #class char-parse-status-me kill;
        #class char.GetStatusMe kill;
        event.UnHandle GA {char.status_me};
        #delay 0 {okLog 人物状态信息已更新。};
        event.Emit char/status;
    };

    event.HandleOnce GA {char.status_me} {char} {
        char.status_me.parse.done;
        #nop 吞参数专用，不要删除本行，也不要在末尾加分号或是别的语句
    };

    #class char-parse-status-me close;
};

#alias {char.GetStatusMe} {
    #local silent {%1};
    #local args   {%2};

    #class char.GetStatusMe open;

#nop ╭───个人状态─────┬────────────┬─────────────╮;
    #action {^╭─{(─)*}─个人状态─{((─)+┬){2}}──{(─)*}─╮$} {
        #if { "%1" == "gag" } {#line gag};
        #class char.GetStatusMe kill;
        char.status_me.parse.output %1;
    };

#nop │当头炮(Dtp)             │级别:64                 │战斗力:99999999           │;
    #action {^│%S(%S)%s│级别:%d%s│战斗力:%d%s│$} {
        #if { "%1" == "gag" } {#line gag};
        #class char.GetStatusMe kill;
        char.status_me.parse.output %1;
    };

    #action {^等等，系统喘气中......$} {
        #class char.GetStatusMe kill;
    };

    #class char.GetStatusMe close;

    xtt.Send status_me %2;
};

#alias {status_me}  {char.GetStatusMe gag   {%0}};
#alias {sm}         {char.GetStatusMe nogag {%0}};

#delay status_me.Init {char.GetStatusMe gag} 3;
