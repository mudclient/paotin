#nop vim: set filetype=tt:;

/*
本文件属于 PaoTin++ 的一部分
===========
PaoTin++ © 2020~2023 的所有版权均由担子炮(dzp <danzipao@gmail.com>) 享有并保留一切法律权利
你可以在遵照 GPLv3 协议的基础之上使用、修改及重新分发本程序。
===========
*/

#var basic_char_hp[META] {
    {NAME}      {HP 信息解析}
    {DESC}      {解析 hp 和 hpbrief 两个命令的内容，并储存到变量 char[HP]}
    {AUTHOR}    {担子炮}
    {NOTE}      {本文件属于 PaoTin++ 的一部分}
};

load-lib event;
load-lib speedo;

event.Define {char/hp}       {无参}  {$MODULE} {已经获取到 hp 命令输出结果，并更新 char[HP]。};
event.Define {char/hpbrief}  {无参}  {$MODULE} {已经获取到 hpbrief 命令输出结果，并更新 char[HP]。};

#func {basic_char_hp.Init} {
    #class data/basic/char open;
    #var char[HP]               {};
    #var char[HP][经验]         {1};
    #var char[HP][经验显示]     {1};
    #var char[HP][潜能]         {1};
    #var char[HP][潜能显示]     {1};
    #var char[HP][当前内力]     {0};
    #var char[HP][最大内力]     {1};
    #var char[HP][加力]         {0};
    #var char[HP][内力百分比]   {0};
    #var char[HP][当前精力]     {0};
    #var char[HP][最大精力]     {1};
    #var char[HP][精力百分比]   {0};
    #var char[HP][当前气血]     {0};
    #var char[HP][有效气血]     {1};
    #var char[HP][最大气血]     {1};
    #var char[HP][气血百分比]   {0};
    #var char[HP][气血健康度]   {0};
    #var char[HP][当前精神]     {0};
    #var char[HP][有效精神]     {1};
    #var char[HP][最大精神]     {1};
    #var char[HP][精神百分比]   {0};
    #var char[HP][精神健康度]   {0};
    #var char[HP][当前真气]     {0};
    #var char[HP][最大真气]     {1};
    #var char[HP][真气减伤]     {0};
    #var char[HP][真元]         {0};
    #var char[HP][食物]         {0};
    #var char[HP][最大食物]     {1};
    #var char[HP][饥饿]         {};
    #var char[HP][饮水]         {0};
    #var char[HP][最大饮水]     {1};
    #var char[HP][口渴]         {};
    #var char[HP][气势]         {0};
    #var char[HP][状态]         {};
    #var char[HP][毒]           {};
    #class data/basic/char close;

    set hpbrief long,report;
    #delay {char.Init} {char.GetHP; char.GetHP2} {1};

    #return true;
};

/*
hp 有两种格式，非战斗状态：
┌───个人状态────────────┬───────────────────┐
│【精神】 5978    / 5978     [103%]    │【精力】 12102   / 6051    (+   0)    │
│【气血】 23372   / 23372    [101%]    │【内力】 5369    / 7063    (+   1)    │
│【真气】 1367    / 1367     [  9%]    │【静气】 101%               [正常]    │
│【食物】 874     / 900      [正常]    │【潜能】 7.67万                       │
│【饮水】 874     / 900      [正常]    │【经验】 1349.88万                    │
├───────────────────┴───────────────────┤
│【状态】 健康、怒、星宿火毒、星宿毒掌毒                                       │
└──────────────────────────────北大侠客行────┘
战斗状态：
*/
#alias {char.hp.parse.hp} {
    #class char-hp-parse-hp open;
    #action {^│【精神】%s%d%s/%s%d%s[%s%d\%]%!s│【精力】%s%d%s/%s%d%s(+%s%d)%!s│$} {
        #var char[HP][当前精神] {%%2};
        #var char[HP][有效精神] {%%5};
        #var char[HP][当前精力] {%%10};
        #var char[HP][最大精力] {%%13};
        speedo.Set {当前精神} {$char[HP][当前精神]} {true} {10};
        speedo.Set {当前精力} {$char[HP][当前精力]} {true} {10};
    };
    #action {^│【气血】%s%d%s/%s%d%s[%s%d\%]%!s│【内力】%s%d%s/%s%d%s(+%s%d)%!s│$} {
        #var char[HP][当前气血] {%%2};
        #var char[HP][有效气血] {%%5};
        #var char[HP][当前内力] {%%10};
        #var char[HP][最大内力] {%%13};
        #var char[HP][加力]     {%%16};
        speedo.Set {当前气血} {$char[HP][当前气血]} {true} {10};
        speedo.Set {当前内力} {$char[HP][当前内力]} {true} {10};
    };
    #action {^│【真气】%!s%d%!s/%!s%d%!s[%!s%d\%]%!s│【%!*】%!s%!d\%%!s[%!*]%!s│$} {
        #var char[HP][当前真气] {%%1};
        #var char[HP][最大真气] {%%2};
        #var char[HP][真气减伤] {%%3};
        speedo.Set {当前真气} {$char[HP][当前真气]} {true} {10};
    };
    #action {^│【食物】%s%d%s/%s%d%s[%S]%!s│【潜能】%s%S%!s│$} {
        #var char[HP][食物]     {%%2};
        #var char[HP][最大食物] {%%5};
        #var char[HP][饥饿]     {%%7};
        #var char[HP][潜能显示] {%%9};
        speedo.Set {食物} {$char[HP][食物]} {true} {10};
    };
    #action {^│【饮水】%s%d%s/%s%d%s[%S]%!s│【经验】%s%S%!s│$} {
        #var char[HP][饮水]     {%%2};
        #var char[HP][最大饮水] {%%5};
        #var char[HP][口渴]     {%%7};
        #var char[HP][经验显示] {%%9};
        speedo.Set {饮水} {$char[HP][饮水]} {true} {10};
    };
    #action {^│【气势】%s%d%s\%%s　【平衡】%s%d%s\%%!s│$} {
        #var char[HP][气势]     {%%2};
    };
    #action {^├───────────────────────────────────┤$} {
        #nop;
    };
    #action {~【状态】%*│} {
        #local status {%%1};
        #replace status { } {};
        #replace status {{\033\[[0-9;]+m}} {};
        #replace status {、} {;};
        #var char[HP][状态] {$status};
        #local item {};
        #local poison {};
        #foreach {$status} {item} {
            #if { "$item" == "{.*毒|火焰刀焚伤|生死符}" } {
                #local poison {$poison$item;};
            };
        };
        #replace poison {;$} {};
        #var char[HP][毒] {$poison};
    };

    #action {^╰─────────────{(─)*}%S────╯{|ID=char.hp}$} {
        #math char[HP][气血百分比] { $char[HP][当前气血] * 100 / $char[HP][最大气血] };
        #math char[HP][精神百分比] { $char[HP][当前精神] * 100 / $char[HP][最大精神] };
        #math char[HP][内力百分比] { $char[HP][当前内力] * 50  / $char[HP][最大内力] };
        #math char[HP][精力百分比] { $char[HP][当前精力] * 50  / $char[HP][最大精力] };
        #math char[HP][气血健康度] { $char[HP][有效气血] * 100 / $char[HP][最大气血] };
        #math char[HP][精神健康度] { $char[HP][有效精神] * 100 / $char[HP][最大精神] };
        event.Emit char/hp;
        char.hp.parse.done;
    };

    #alias {char.hp.parse.done} {
        #class char-hp-parse-hp kill;
        #class char.GetHP kill;
        event.UnHandle GA {char.hp};
    };

    event.HandleOnce GA {char.hp} {char} {
        char.hp.parse.done;
        #nop 吞参数专用，不要删除本行，也不要在末尾加分号或是别的语句
    };

    #class char-hp-parse-hp close;
};

/*
hp -neili 格式：
┌─其他信息────────────┬─────────────────┐
│【走火入魔】 0.00  %              │【极限内力】 0                    │
└─────────────────┴─────────北大侠客行───┘
┌──内力显示─────────────────────────────┐
│□太极神功 (taiji-shengong)                 　 -   2117/  5109(6040)  │
├──其他信息───────────┬─────────────────┤
│【走火入魔】 -41.50%              │【极限内力】 6040                 │
└─────────────────┴─────────北大侠客行───┘
┌─内力显示──────────────────────────────┐
│  北冥神功 (beiming-shengong)               　 -   8385/  8385(8578)  │
│□逍遥派内功 (xiaoyao-neigong)              　 -   2865/  2865(4480)  │
│  观花诀 (guanhua-jue)                      　 -   1695/  1695(4662)  │
├─其他信息────────────┬─────────────────┤
│【走火入魔】 -1.13 %              │【极限内力】 11613                │
└─────────────────┴─────────北大侠客行───┘
*/

#alias {char.hp.parse.hp-neili} {
    #class char-hp-parse-hp-neili open;

    #var char[HP][内力上限] {1};

    #action {^│{□|  }%S (%S)%s　 - %s%d/%s%d(%d)%s│$} {
        #local name     {@str.Trim{%%2}};
        #local id       {@str.Trim{%%3}};
        #local current  {@str.Trim{%%6}};
        #local max      {@str.Trim{%%8}};
        #local limit    {@str.Trim{%%9}};

        #if { "%%1" == "□" } {
            #local name {$char[Skills][基本内功][jifa-to]};
            #var char[HP][当前内力] {$current};
            #var char[HP][最大内力] {$max};
            #var char[HP][内力上限] {$limit};
        };

        #if { "$char[Skills][$name]" != "" } {
            #var char[Skills][$name][neili]         {$current};
            #var char[Skills][$name][max-neili]     {$max};
            #var char[Skills][$name][neili-limit]   {$limit};
        };
    };

    #action {~^%*【恢复效率】%*%+1..c{[0-9.]+}\%%*$} {
        #echo {%s} {@Beautify{%%1【恢复效率】%%2%%3@str.Left{{@str.Format{{%.2f};@math.Eval{%%4 / 100}}     };@math.Eval{@str.Width{%%4}}} %%5}};
        #line gag;
    };

    #action {^│【走火入魔】 %*% %s│【极限内力】 %d%s │$} {
        #var char[HP][走火入魔] {@str.Trim{%%1}};
    };

    #action {^└─────────────{(─)*}%S─{(─)*}─┘{|ID=char.hp-neili}$} {
        char.hp-neili.parse.done;
    };

    #alias {char.hp-neili.parse.done} {
        #local __unused {%%0};
        #class char-hp-parse-hp-neili kill;
        #class char.GetHP kill;
        event.UnHandle GA {char.hp-neili};
    };

    event.HandleOnce GA {char.hp-neili} {char} {char.hp-neili.parse.done};

    #class char-hp-parse-hp-neili close;
};

#nop hpbrief 格式：
#nop 第一行: 经验，潜能，最大内力，当前内力，最大精力，当前精力
#nop 第二行: 最大气血，有效气血，当前气血，最大精神，有效精神，当前精神
#nop 第三行: 真气，真元，食物，饮水，是否战斗，是否busy
#nop #69407,853,992,992,844,1398
#nop #771,776,776,397,397,397
#nop #0,0,30,43
#nop;

#nop 第一行: 经验，潜能，最大内力，当前内力，最大精力，当前精力;
#nop 第二行: 最大气血，有效气血，当前气血，最大精神，有效精神，当前精神;
#action {^{|>|> }#%1,%2,%3,%4,%5,%6$} {
    #if { "${char.parsingHP}" == "" } {
        #var char[HP][经验显示] {%1};
        #var char[HP][经验]     {@__char_hp_expandBigNumber__{%1}};
        #var char[HP][潜能显示] {%2};
        #var char[HP][潜能]     {@__char_hp_expandBigNumber__{%2}};
        #var char[HP][最大内力] {@__char_hp_expandBigNumber__{%3}};
        #var char[HP][当前内力] {@__char_hp_expandBigNumber__{%4}};
        #var char[HP][最大精力] {@__char_hp_expandBigNumber__{%5}};
        #var char[HP][当前精力] {@__char_hp_expandBigNumber__{%6}};

        speedo.Set {经验}       {$char[HP][经验]}       {true} {10};
        speedo.Set {潜能}       {$char[HP][潜能]}       {true} {10};
        speedo.Set {当前内力}   {$char[HP][当前内力]}   {true} {10};
        speedo.Set {当前精力}   {$char[HP][当前精力]}   {true} {10};

        #math char[HP][内力百分比] { $char[HP][当前内力] * 50 / $char[HP][最大内力] };
        #math char[HP][精力百分比] { $char[HP][当前精力] * 50 / $char[HP][最大精力] };

        #var char.parsingHP     {true};
        #line gag;
    };
    #else {
        #var char[HP][最大气血] {@__char_hp_expandBigNumber__{%1}};
        #var char[HP][有效气血] {@__char_hp_expandBigNumber__{%2}};
        #var char[HP][当前气血] {@__char_hp_expandBigNumber__{%3}};
        #var char[HP][最大精神] {@__char_hp_expandBigNumber__{%4}};
        #var char[HP][有效精神] {@__char_hp_expandBigNumber__{%5}};
        #var char[HP][当前精神] {@__char_hp_expandBigNumber__{%6}};

        speedo.Set {当前气血} {$char[HP][当前气血]} {true} {10};
        speedo.Set {当前精神} {$char[HP][当前精神]} {true} {10};

        #math char[HP][精神百分比] { $char[HP][当前精神] * 100 / $char[HP][最大精神] };
        #math char[HP][气血百分比] { $char[HP][当前气血] * 100 / $char[HP][最大气血] };

        #unvar char.parsingHP;
        #line gag;
    };
} {2.1};

#nop 第三行: 真气，真元，食物，饮水;
#action {^#%1,%2,%3,%4,{0|1},{0|1}$} {
    #var char[HP][当前真气]  {@__char_hp_expandBigNumber__{%1}};
    #var char[HP][当前真元]  {@__char_hp_expandBigNumber__{%2}};
    #var char[HP][食物]      {@__char_hp_expandBigNumber__{%3}};
    #var char[HP][饮水]      {@__char_hp_expandBigNumber__{%4}};
    #var char[HP][战斗中]    {false};
    #var char[HP][忙]        {false};

    speedo.Set {当前真气}   {$char[HP][当前真气]}   {true} {10};
    speedo.Set {食物}       {$char[HP][食物]}       {true} {10};
    speedo.Set {饮水}       {$char[HP][饮水]}       {true} {10};

    #if { "%5" == "1" } {
        #var char[HP][战斗中] {true};
    };

    #if { "%6" == "1" } {
        #var char[HP][忙] {true};
    };

    event.Emit char/hpbrief;

    #unvar char.parsingHP;

    #delay {char.HPSummarize} {char.HPSummarize} {0};

    #line gag;
} {2.0};

#alias {char.HPSummarize} {
    #local color    {<168>};
    #local combat   {};
    #local safety   {};

    #if { $char[HP][气血百分比] < 30 || $char[HP][精神百分比] < 70 } {
        #local safety {危险};
    };
    #elseif { $char[HP][气血百分比] > 80 && $char[HP][精神百分比] > 95 } {
        #local safety {安全};
    };

    #if { "$char[HP][战斗中]" == "true" } {
        #if { "$safety" == "安全" } {
            #format color   {<B040><128>};
            #format combat  { <118>战 <128>安<298>};
        };
        #elseif { "$safety" == "危险" } {
            #format color   {<B400><118>};
            #format combat  { <118>战 <118>危<298>};
        };
        #else {
            #format color   {<218>};
            #format combat  { <118>战<298>};
        };
    };

    #if { "$char[HP][忙]" == "true" } {
        #format color   {<118>};
        #format combat  {$combat <138>忙<298>};
    };

    #local 每秒气血变化 {@speedo.GetSpeed{当前气血}};
    #if { ${每秒气血变化} > 0 } {
        #local 每秒气血变化 {(<128>${每秒气血变化}<298>)};
    };
    #elseif { ${每秒气血变化} < 0 } {
        #local 每秒气血变化 {(<118>${每秒气血变化}<298>)};
    };
    #else {
        #local 每秒气血变化 {};
    };

    #local 每秒精神变化 {@speedo.GetSpeed{当前精神}};
    #if { ${每秒精神变化} > 0 } {
        #local 每秒精神变化 {(<128>${每秒精神变化}<298>)};
    };
    #elseif { ${每秒精神变化} < 0 } {
        #local 每秒精神变化 {(<118>${每秒精神变化}<298>)};
    };
    #else {
        #local 每秒精神变化 {};
    };

    #local 每秒内力变化 {@speedo.GetSpeed{当前内力}};
    #if { ${每秒内力变化} > 0 } {
        #local 每秒内力变化 {(<128>${每秒内力变化}<298>)};
    };
    #elseif { ${每秒内力变化} < 0 } {
        #local 每秒内力变化 {(<118>${每秒内力变化}<298>)};
    };
    #else {
        #local 每秒内力变化 {};
    };

    #local 每秒精力变化 {@speedo.GetSpeed{当前精力}};
    #if { ${每秒精力变化} > 0 } {
        #local 每秒精力变化 {(<128>${每秒精力变化}<298>)};
    };
    #elseif { ${每秒精力变化} < 0 } {
        #local 每秒精力变化 {(<118>${每秒精力变化}<298>)};
    };
    #else {
        #local 每秒精力变化 {};
    };

    #nop 气血恢复的数值展示为每分钟可以恢复的最大血量的倍数（以百分比形式）;
    #local {气血恢复} {@math.Int{@math.Eval{$char[STATUS][气血恢复] * 100 * 60 / $char[HP][最大气血]}}};

    #nop 气血治疗的数值展示为每分钟可以治疗的最大血量的倍数（以百分比形式）;
    #local {气血治疗} {@math.Int{@math.Eval{$char[STATUS][气血治疗] * 100 * 60 / $char[HP][最大气血]}}};

    #local summarize {};
    #if { "$char[HP][战斗中]" == "true" } {
        #format summarize {<099>$color〔HP摘要〕<298>气血: %s%s/%s 内力: %s%s 回气: %s/%s 精神: %s%s/%s 食水: %s/%s$combat<099>}
            {@__char_hp_colorit__{$char[HP][气血百分比]}} {${每秒气血变化}} {@__char_hp_colorit__{$char[HP][气血健康度]}}
            {@__char_hp_colorit__{$char[HP][内力百分比]}} {${每秒内力变化}}
            {@__char_hp_colorit__{${气血恢复}}} {@__char_hp_colorit__{${气血治疗}}}
            {@__char_hp_colorit__{$char[HP][精神百分比]}} {${每秒精神变化}} {@__char_hp_colorit__{${精神健康度}}}
            {$char[HP][食物]} {$char[HP][饮水]}
    };
    #else {
        #local jingli {@if{$chat[HP][精力百分比] < 30;{@__char_hp_colorit__{$char[HP][精力百分比]} ${每秒精力变化}}}};
        #format summarize {<099>$color〔HP摘要〕<298>气血: %s%s/%s 内力: %s%s 回气: %s/%s 精神: %s%s/%s $jingli食水: %s/%s(%s/%s)$combat<099>}
            {@__char_hp_colorit__{$char[HP][气血百分比]}} {${每秒气血变化}} {@__char_hp_colorit__{$char[HP][气血健康度]}}
            {@__char_hp_colorit__{$char[HP][内力百分比]}} {${每秒内力变化}}
            {@__char_hp_colorit__{${气血恢复}}} {@__char_hp_colorit__{${气血治疗}}}
            {@__char_hp_colorit__{$char[HP][精神百分比]}} {${每秒精神变化}} {@__char_hp_colorit__{${精神健康度}}}
            {$char[HP][食物]} {$char[HP][饮水]}
            {@speedo.GetSpeed{食物;60}} {@speedo.GetSpeed{饮水;60}};
    };

    #showme $summarize;
};

#func {__char_hp_expandBigNumber__} {
    #local number {%1};
    #replace number {K} {*1000};
    #replace number {M} {*1000000};
    #math number {$number};
    #return $number;
};

#func {__char_hp_colorit__} {
    #local percent {@math.Int{@defaultNum{%1;0}}};
    #return {@util.Grade{$percent;<218>;15;<118>;30;<238>;60;<138>;90;<128>;101;<168>}$percent%<298>};
};

#alias {char.GetHP} {
    #class char.GetHP open;

    #nop ╭───个人状态────────────┬───────────────────╮;
    #action {^╭─{(─)*}─个人状态─{((─)+┬){1}}──{(─)*}─╮$} {
        #class char.GetHP kill;
        char.hp.parse.hp;
    };

    #nop ┌─内力显示──────────────────────────────┐;
    #action {^┌─内力显示──{(─)*}───────┐$} {
        #class char.GetHP kill;
        char.hp.parse.hp-neili;
    };

    #action {~^%c【状态】%*\e[2;37;0m{?:|、(.*)\x1b\[2;37;0m}%s%c$} {
        #var char[STATUS][健康状态] {%%2};
        #var char[STATUS][持续效果] {%%3};
        event.DelayEmit char/status;
        #class char.GetHP kill;
    } {4};

    #action {^你用HP太频繁了，请尽量使用hpbrief命令，节约系统资源，谢谢。$} {
        #class char.GetHP kill;
    };

    #class char.GetHP close;

    xtt.Send {hp %0};
};

#alias {char.GetHP2} {
    tune gmcp status off;
    hpbrief;
    tune gmcp status on;
};

#alias {hp} {
    #if { "%0" == "{|-neili|-status}" } {
        char.GetHP %0;
    };
    #else {
        xtt.Send hp %0;
    };
};

///=== {
// #@ char.IsBusy
//    如果角色正忙，则返回真，否则返回假。无参数。
// };
#func {char.IsBusy} {
    #if { "$char[HP][忙]" == "true" } {
        #return 1;
    };
    #else {
        #return 0;
    };
};

///=== {
// #@ char.IsIdle
//    如果角色不忙，则返回真，否则返回假。无参数。
// };
#func {char.IsIdle} {
    #if { "$char[HP][忙]" == "false" } {
        #return 1;
    };
    #else {
        #return 0;
    };
};

///=== {
// #@ char.InCombat
//    如果角色正处于战斗中，则返回真，否则返回假。无参数。
// };
#func {char.IsIdle} {
    #if { "$char[HP][战斗中]" == "true" } {
        #return 1;
    };
    #else {
        #return 0;
    };
};
